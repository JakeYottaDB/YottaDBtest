V1RANDA	;$RANDOM FUNCTION -1-;YS-KO-TS,,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1978
	W !!,"V1RANDA: TEST $RANDOM FUNCTION -1-",!
	W !,"THIS ROUTINE TAKES MUCH TIME.  PLEASE BE PATIENT",!
	W !,"$RANDOM(intexpr)",!
738	W !,"I-738  randomness of $RANDOM(10)  (visual)",!
	S ITEM="I-738  "
	W ?14,"THIS TEST PRODUCES 2 STRINGS OF 65 DIGITS EACH BY"
	W !,"SUCCESSIVE CONCATINATIONS OF $R(10).  TWO SIMILAR STATISTICS"
	W !,"ARE THEN CALCULATED FROM THESE TWO RANDOM STRINGS AS TESTS OF"
	W !,"RANDOMNESS:    (1) THE NUMBER OF OCCURRENCES OF IDENTICAL 10 DIGIT"
	W !?19,"SUBSTRINGS WITHIN BOTH STRINGS",!?15,"(2) THE NUMBER OF SUCH "
	W "5 DIGIT SUBSTRINGS"
	S X="",Y=""
	F I=1:1:65 S X=$R(10)_X,Y=$R(10)_Y
	W !!,X,!,Y,!!,"       SUBSTRING     POSITION   POSITION",!?21,"1ST STRING"
	W ?32,"2ND STRING"
	S Z1=10 D EXT S Z1=5 D EXT
	;
739	W !!,"I-739  interpretation of intexpr  (visual)"
	S ITEM="I-739  "
110	W !,"       $R(1E+1) : "
	F I=1:1:5 S R=$RANDOM(1E+1) W R," " I R'?.N!(R'<10) W "ERROR "
120	W !,"       $R(105.5E-1) : "
	F I=1:1:5 S R=$R(105.5E-1) W R," " I R'?.N!(R'<10) W "ERROR "
130	W !,"       $R(""10000A"") : "
	F I=1:1:5 S R=$R("10000A") W R," " I R'?.N!(R'<10000) W "ERROR "
140	W !,"       $R(--100/9.9) : "
	F I=1:1:5 S R=$R(--100/9.9) W R," " I R'?.N!(R'<10) W "ERROR "
160	W !,"       $R(1) : " F I=1:1:5 S R=$R(1) W R," " I R'="0" W "ERROR "
	;
740	W !!,"I-740  intexpr is 9 digits ( maximum range )  (visual)"
	S ITEM="I-740  "
150	W !,"       $R(999999999) : "
	F I=1:1:5 S R=$R(999999999) W R," " I R'?.N!(R'<999999999) W "ERROR "
	;
741	W !!,"I-741  range of returned value (transition test)  (visual)"
	S ITEM="I-741  "
INTER	W !!,"FOLLOWING THREE TESTS ARE EXAMINED WITH 'CHI SQUARE' TEST"
	W !,"EACH EXPERIMENTED VALUE OF 'CHI SQUARE' IS REQUIRED"
	W !,"TO BE WITHIN 95% CONFIDENCE VALUE"
	W !,"TRANSITION TEST"
Y1	K  S CHI=0 F I=0:1:9 F J=0:1:9 S A(I,J)=0
Y2	S I=$R(10) F K=1:1:3000 S J=$R(10),A(I,J)=A(I,J)+1,I=J
Y3	W !!?4,":" F J=0:1:9 W ?J+1*7,J
Y4	W ! F K=1:1:36 W " -"
Y5	F I=0:1:9 W !,I,?4,":" F J=0:1:9 W ?J+1*7,A(I,J)
Y6	F I=0:1:9 F J=0:1:9 S CHI=(A(I,J)-30)*(A(I,J)-30)/30+CHI
Y7	W !!,"95% CONFIDENCE VALUE : LESS THAN 123.2"
Y8	W !,"VALUE OF 'CHI SQUARE' : ",$J(CHI,0,2)
Y9	I CHI>123.2 W "   OUT OF LIMITS"
	;Y7 W !!,"95% CONFIDENCE VALUE : LESS THAN 127.9"
	;Y8 W !,"VALUE OF 'CHI SQUARE' : ",$J(CHI,0,2)
	;Y9 I CHI>127.9 W "   OUT OF LIMITS"
	;
END	W !!,"END OF V1RANDA",!
	S ROUTINE="V1RANDA",TESTS=4,AUTO=0,VISUAL=4 D ^VREPORT
	K  Q
	;
EXT	W !!,"       ( ",Z1," DIGITS)"
	F I=1:1:65-Z1+1 S Z=0,X1=$E(X,I,I+Z1-1) D FIND
	Q
FIND	S Z=$F(Y,X1,Z) I 'Z=0 W !?7,X1,?21,I,?32,Z-Z1 G FIND
	Q
