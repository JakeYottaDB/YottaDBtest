V1PCB	;POST-CONDITIONALS -2-;YS-MM-TS,V1PC,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1978
	S PASS=0,FAIL=0
	W !!,"V1PCB: TEST OF POST-CONDITIONALS -2-",!
716	W !,"I-716  DO command ( command word )"
	S ITEM="I-716  ",VCOMP="" I 1
	DO:0 1 S VCOMP=VCOMP_$T_" " I 0 ;COMMENT
	D:1 10 S VCOMP=VCOMP_$T_" "
	S VCORR="1 10 0 " D EXAMINER
	;
717	W !,"I-717  DO command ( argument )"
	S ITEM="I-717  ",VCOMP="" I 1
	DO ARG1:0 S VCOMP=VCOMP_$T_" " I 0 ;COMMENT
	D ARG2:1 S VCOMP=VCOMP_$T_" "
	S VCORR="1 ARG2 0 " D EXAMINER
	;
842	W !,"I-842  DO with postconditionalized label list"
	S ITEM="I-842  ",VCOMP=""
	S V1PC="$"
	D:V1PC="$" %0,%1:1,%2:$L(V1PC)=1,%3:$E(V1PC,2)="$" D:V1PC %01,%02:1,%03
	S VCORR="%0 Z0 %1 %2 " D EXAMINER
	;
843	W !,"I-843  DO with postconditionalized label+intexpr list"
	S ITEM="I-843  ",VCOMP="" K A
	D BYTE+2:$D(A),BYTE+1:'$D(A)
	S VCORR="BYTE+1 " D EXAMINER
	;
844	W !,"I-844  DO with postconditionalized ^routineref list"
	S ITEM="I-844  ",VCOMP=""
	S A=1,I=1 D:A ^V1PC1 S I=2 D ^V1PC1:A
	S I=3 D ^V1PC1:A=0 S I=4 D:A ^V1PC1:A,^V1PC1:'A,^V1PC1:I=4
	S VCORR="^V1PC11 ^V1PC12 ^V1PC14 ^V1PC14 " D EXAMINER
	;
845	W !,"I-845  DO with postconditionalized label^routineref list"
	S ITEM="I-845  ",VCOMP=""
	D OS^V1PC1:2=2=1,FILE^V1PC1:$J("",2)="  ",010^V1PC1:3=3=3
	S VCORR="OS FILE " D EXAMINER
	;
846	W !,"I-846  DO with postconditionalized label+intexpr^routineref list"
	S ITEM="I-846  ",VCOMP="" K ^V1PC1
	S ^V1PC1=10
	D OS+1^V1PC1:^V1PC1=1,OS+2^V1PC1:^V1PC1=10.0,OS+3^V1PC1:^V1PC1-5-5
	DO:^V1PC1=1 OS+4^V1PC1:1,OS+4^V1PC1:"ABC"="ABC"
	D:10=^V1PC1 ABCDEFGH+1^V1PC1:$F(0.234560,0)=0,ABCDEFGH+2^V1PC1:$D(^V1PC1(1))=1
	S X=13,Y=14,^V1PC1=1
	D O^V1PC1,O+1^V1PC1,O^V1PC1:X=15,Q^V1PC1:$D(X),A:0,R^V1PC1:$E("ABCDEFGH",1,3)="ABC",S^V1PC1:X=13=(Y=14)
	D:$D(X) T^V1PC1:^V1PC1=1,T+1^V1PC1,T^V1PC1:X=15,V^V1PC1
	D:$D(^V1PC1)=0 T^V1PC1:X=13,T+1^V1PC1,T^V1PC1:X=15,U^V1PC1
	S VCORR="OS+2 ABCDEFGH+1 OPQRSTUV" D EXAMINER
	;
718	W !,"I-718  KILL command"
	S ITEM="I-718.1  local",VCOMP="" K X,Y
	S X=13,X(1)=1,X(1,2)=12,X(1,2,3)=123,X(1,2,1)=121,X(3)=3
	S Y=1,Y(2,1)=21 S VCOMP=VCOMP_$D(Y)_" "
	KILL:X=13 Y S VCOMP=VCOMP_$D(Y)_" "
	S VCOMP=VCOMP_$D(X(1,2))_" "
	K:$D(X(1))=11 X(1,2) S VCOMP=VCOMP_$D(X(1,2))_" "
	K:$D(X) X S VCOMP=VCOMP_$D(X)
	S VCORR="11 0 11 0 0" D EXAMINER
	;
	S ITEM="I-718.2  global",VCOMP="" K X,Y,^V1
	S ^V1=1,^V1(1,2)=12,^V1(1,2,2)=122
	S VCOMP=VCOMP_$D(^V1(1,2))_" "
	K:^V1(1,2,2)=122 ^V1(1,2) S VCOMP=VCOMP_$D(^V1(1,2))_" "
	K:$L("ABD")>2 ^V1 S VCOMP=VCOMP_$D(^V1)
	S VCORR="11 0 0" D EXAMINER
	;
END	W !!,"END OF V1PCB",!
	S ROUTINE="V1PCB",TESTS=9,AUTO=9,VISUAL=0 D ^VREPORT
	K  K ^V,^V1PC1,^V1 Q
	;
A	S VCOMP=VCOMP_"A " Q ;846
%01	S VCOMP=VCOMP_"%01 " D Z01 Q  ;842
%0	S VCOMP=VCOMP_"%0 " D Z0 Q  ;842
Z01	S VCOMP=VCOMP_"Z01 " Q  ;842
Z0	S VCOMP=VCOMP_"Z0 " Q  ;842
%02	S VCOMP=VCOMP_"%02 " Q  ;842
%03	S VCOMP=VCOMP_"%03 " Q  ;842
%2	S VCOMP=VCOMP_"%2 " Q  ;842
%1	S VCOMP=VCOMP_"%1 " Q  ;842
%3	S VCOMP=VCOMP_"%3 " Q  ;842
10	S VCOMP=VCOMP_"10 " Q  ;716
ARG1	S VCOMP=VCOMP_"ARG1 " Q  ;717
1	S VCOMP=VCOMP_"1 " Q  ;716
ARG2	S VCOMP=VCOMP_"ARG2 " Q  ;717
BYTE	S VCOMP=VCOMP_"BYTE " Q  ;843
	S VCOMP=VCOMP_"BYTE+1 " Q  ;843
	S VCOMP=VCOMP_"BYTE+2 " Q  ;843
	S VCOMP=VCOMP_"BYTE+3 " Q  ;843
	;
EXAMINER	I VCORR=VCOMP S PASS=PASS+1 W !,"   PASS  ",ITEM W:$Y>55 # Q
	S FAIL=FAIL+1 W !,"** FAIL  ",ITEM W:$Y>55 #
	W !,"           COMPUTED =""",VCOMP,"""" W:$Y>55 #
	W !,"           CORRECT  =""",VCORR,"""" W:$Y>55 #
	Q
