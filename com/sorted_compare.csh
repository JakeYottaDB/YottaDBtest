#!/usr/local/bin/tcsh -f
#################################################################
#								#
# Copyright (c) 2014-2015 Fidelity National Information 	#
# Services, Inc. and/or its subsidiaries. All rights reserved.	#
#								#
#	This source code contains the intellectual property	#
#	of its copyright holder(s), and is made available	#
#	under a license.  If you do not know the terms of	#
#	the license, please stop and do not read further.	#
#								#
#################################################################

# A tool to compare (cmp) two files and sort if needed
# usage : $0 <file1> <file2>

set sortopt = ""
set file1 = "$1"
set file2 = "$2"
set stat = 1
if ( (! -e $file1) || (! -e $file2) ) then
	echo "either $file1 or $file2 does not exist"
	exit 1
endif

if (! $?gtm_test_trigdiff_exact) then
	if (($file1:e == "trg") || ($file2:e == "trg")) then
		# input files are .trg files which could be generated by a MUPIP TRIGGER -SELECT
		# If gtm_test_spanreg is 1, then it is possible the global names get mapped to different regions
		# on primary and secondary so an RF_EXTR call that extracts triggers on the two sides could see
		# a different in the region name which occurs in the comment line of a trigger select.
		# An example line is of the form
		#	;trigger name: bard#1 (region DREG)  cycle: X
		# In such a line mask out the region name so it does not affect the diff.
		#
		# Since a trigger may be installed in differing regions on each side, use 'sort -u' to avoid
		# differences in the order of the outputted triggers
		#
		# imptp.m randomly installs or deletes the trigger triggernameforinsertsanddels depending on the
		# randomness, it might be present in any side (not just the supplementary side) remove that
		# trigger before comparing. This matches what test/com_u/RF_EXTR_supplinst.csh does
		$grep -vE "triggernameforinsertsanddels|imptpdztrig" $file1 | \
			sed 's/^\(;.*(region \).*\().*\)/\1\2/g' > $file1.filter
		$grep -vE "triggernameforinsertsanddels|imptpdztrig" $file2 | \
			sed 's/^\(;.*(region \).*\().*\)/\1\2/g' > $file2.filter
		set file1 = $file1.filter
		set file2 = $file2.filter
		set sortopt = "-u"
	endif
endif

$tst_cmpsilent $file1 $file2
set stat = $status
if ($stat) then
	sort $sortopt $file1 >&! ${file1}.sort
	sort $sortopt $file2 >&! ${file2}.sort
	cmp ${file1}.sort ${file2}.sort
	set stat = $status
endif

exit $stat
