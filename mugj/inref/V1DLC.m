V1DLC	;$DATA, KILL (LOCAL VARIABLES) -4-;YS-TS,,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1978
	S ^PASS=0,^FAIL=0
	W !!,"V1DLC : TEST OF $DATA FUNCTION AND KILL COMMAND ON LOCAL VARIABLES -4-",!
231	W !,"I-231  Selective KILL"
	S ^ITEM="I-231  " K
	S X(1,2)="X12",Y(3)="Y3",Z="Z",Z(0)="Z0",W="W",VCOMP="" D DATA
	K Y D DATA S Y="Y" D DATA
	S VCORR="10 10 1 10 1 11 1 1 ***X12**Y3*Z*Z0*W*/10 10 1 0 0 11 1 1 ***X12***Z*Z0*W*/10 10 1 1 0 11 1 1 ***X12*Y**Z*Z0*W*/" D EXAMINER
	;
232	W !,"I-232  Exclusive KILL with argument list"
	S ^ITEM="I-232  " K
	S X(1,2)="X12",Y(3)="Y3",Z="Z",Z(0)="Z0",W="W",VCOMP="" D DATA S ^VCOMP=VCOMP
	K (X,Y,Z),(X,W) S VCOMP=^VCOMP D DATA
	S VCORR="10 10 1 10 1 11 1 1 ***X12**Y3*Z*Z0*W*/10 10 1 0 0 0 0 0 ***X12******/" D EXAMINER
	;
233	W !,"I-233  Exclusive KILL with one argument"
	S ^ITEM="I-233  " K
	S X(1,2)="X12",Y(3)="Y3",Z="Z",Z(0)="Z0",W="W",VCOMP="" D DATA S ^VCOMP=VCOMP
	K (X,Y,Z) S VCOMP=^VCOMP D DATA
	S VCORR="10 10 1 10 1 11 1 1 ***X12**Y3*Z*Z0*W*/10 10 1 10 1 11 1 0 ***X12**Y3*Z*Z0**/" D EXAMINER
	;
234	W !,"I-234  Exclusive KILL, which lvn is not defined"
	S ^ITEM="I-234  " K
	S X(1,2)="X12",Y(3)="Y3",Z="Z",Z(0)="Z0",W="W",VCOMP="" D DATA S ^VCOMP=VCOMP
	K (A,B,C) S VCOMP=^VCOMP D DATA
	S VCORR="10 10 1 10 1 11 1 1 ***X12**Y3*Z*Z0*W*/0 0 0 0 0 0 0 0 *********/" D EXAMINER
	;
235	W !,"I-235  Mixture of selective KILL and exclusive KILL in one argument"
	S ^ITEM="I-235  " K
	S X(1,2)="X12",Y(3)="Y3",Z="Z",Z(0)="Z0",W="W",VCOMP="" D DATA S ^VCOMP=VCOMP
	K (X,W),Z S VCOMP=^VCOMP D DATA
	S VCORR="10 10 1 10 1 11 1 1 ***X12**Y3*Z*Z0*W*/10 10 1 0 0 0 0 1 ***X12*****W*/" D EXAMINER
	;
END	W !!,"END OF V1DLC",!
	S PASS=^PASS,FAIL=^FAIL
	S ROUTINE="V1DLC",TESTS=5,AUTO=5,VISUAL=0 D ^VREPORT
	K  K ^ITEM,^PASS,^FAIL,^VCOMP Q
	;
EXAMINER	I VCORR=VCOMP S PASS=^PASS,PASS=PASS+1,^PASS=PASS W !,"   PASS  ",^ITEM W:$Y>55 # Q
	S FAIL=^FAIL,FAIL=FAIL+1,^FAIL=FAIL W !,"** FAIL  ",^ITEM W:$Y>55 #
	W !,"           COMPUTED =""",VCOMP,"""" W:$Y>55 #
	W !,"           CORRECT  =""",VCORR,"""" W:$Y>55 #
	Q
	;
DATA	S VCOMP=VCOMP_$D(X)_" "_$D(X(1))_" "_$D(X(1,2))_" "_$D(Y)_" "
	S VCOMP=VCOMP_$D(Y(3))_" "_$D(Z)_" "_$D(Z(0))_" "_$D(W)_" "
	S VCOMP=VCOMP_"*" I $D(X)#10=1 S VCOMP=VCOMP_X
	S VCOMP=VCOMP_"*" I $D(X(1))#10=1 S VCOMP=VCOMP_X(1)
	S VCOMP=VCOMP_"*" I $D(X(1,2))#10=1 S VCOMP=VCOMP_X(1,2)
	S VCOMP=VCOMP_"*" I $D(Y)#10=1 S VCOMP=VCOMP_Y
	S VCOMP=VCOMP_"*" I $D(Y(3))#10=1 S VCOMP=VCOMP_Y(3)
	S VCOMP=VCOMP_"*" I $D(Z)#10=1 S VCOMP=VCOMP_Z
	S VCOMP=VCOMP_"*" I $D(Z(0))#10=1 S VCOMP=VCOMP_Z(0)
	S VCOMP=VCOMP_"*" I $D(W)#10=1 S VCOMP=VCOMP_W
	S VCOMP=VCOMP_"*/" Q
