VV2PAT1	;PATTERN MATCH OPERATOR -1-;KO-TS,,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1984
	S PASS=0,FAIL=0
	W !!,"VV2PAT1: TEST OF PATTERN MATCH OPERATOR -1-",!
148	W !,"II-148  expr ? .intlit2 patcode"
	S ITEM="II-148  ",VCOMP="",X="ABC"
	S VCOMP=X?.0A_(X?.00001U)_(X?.2A)_(X?.03A)_(X?.4U)_(X?.99999999AE)
	S VCORR="000111" D EXAMINER
	;
149	W !,"II-149  expr ? intlit1.patcode"
	S ITEM="II-149  ",VCOMP="",X="ABC"
	S VCOMP=(X?0.A)_(X?1.U)_(X?00002.A)_(X?03.A)_(X?4.U)_(X?0099900.U)
	S VCORR="111100" D EXAMINER
	;
150	W !,"II-150  expr ? intlit1.intlit2 patcode"
	S ITEM="II-150  ",VCOMP="",X="ABC"
	S VCOMP=VCOMP_(X?000.00000000AA)_(X?0.0001U)_(X?0.2A)_(X?000000.00003U)_(X?0.4A)_(X?00.0000999A)_" "
	S VCOMP=VCOMP_(X?1.1A)_(X?1.2U)_(X?1.3UA)_(X?1.4PU)_(X?1.999CPA)_" "
	S VCOMP=VCOMP_(X?2.2AU)_(X?2.3UP)_(X?2.4UUU)_(X?2.999AP)_" "
	S VCOMP=VCOMP_(X?3.3AUL)_(X?3.4AAA)_(X?3.999AU)_" "
	S VCOMP=VCOMP_(X?4.4A)_(X?4.999A)_(X?999.9999A)
	S VCORR="000111 00111 0111 111 000" D EXAMINER
	;
151	W !,"II-151  '?"
	S ITEM="II-151  ",VCOMP="",X="ABC"
	S VCOMP=X'?1.2A_(X'?0.999A)_(X'?4.5A),VCORR="101" D EXAMINER
	;
152	W !,"II-152  Multi patatom"
	S ITEM="II-152  ",VCOMP="",X="ABC"
	S VCOMP=(X?1.CAULPN.1CAULPN)_(X?1.3AN2.4APC)_(X?3.4A1.U)_(X'?0.AAAAAAAA.5ANNN)
	;
	S X="12AB12AB"
	S VCOMP=VCOMP_(X?1."12"3.4A."2"2.U)_(X?.2"12AB".E)_(X?.2N1."AB"4.CNPALUECNPALUE)_(X?.2E.2E.2E8.8AN)
	S VCOMP=VCOMP_("ABCDEFGHIJKLMNOPQRSTUVWXYZ"?1.E3.5A.3CC2.5NU0.5P4.5UUU1"Z")
	S VCOMP=VCOMP_("0123456789..000..123456789"?3.15N2N.2N1.10".".N."."4.N)
	S VCORR="1100011111" D EXAMINER
	;
153	W !,"II-153  expr ? repcount patcode when expr is empty string"
	S ITEM="II-153  ",VCOMP="",X=""_""
	S VCOMP=VCOMP_(""?.0A)_(""?.1A)_(X?.2P)_(" ")_(X?0.A)_(""?1.A)_(""?00002.A)_" "
	S VCOMP=VCOMP_(""?0.0E)_(""?0.1E)_(""?0.2E)_(X?000000.0009999A)_" "
	S VCOMP=VCOMP_(""?1.1EA)_(""?1.2CPA)_(""?2.2E)
	S VCORR="111 100 1111 000" D EXAMINER
	;
154	W !,"II-154  expr contains control characters"
	S ITEM="II-154  ",VCOMP=""
	F I=1:1:10 S VCOMP=VCOMP_($C(I,I*2,I+100,I*I)?1.5CAULPN1.4CAULPN1.3CAULPNCAULPN1.CAULPN)
	S VCORR="1111111111" D EXAMINER
	;
END	W !!,"END OF VV2PAT1",!
	S ROUTINE="VV2PAT1",TESTS=7,AUTO=7,VISUAL=0 D ^VREPORT
	K  Q
	;
EXAMINER	I VCORR=VCOMP S PASS=PASS+1 W !,"   PASS  ",ITEM W:$Y>55 # Q
	S FAIL=FAIL+1 W !,"** FAIL  ",ITEM W:$Y>55 #
	W !,"           COMPUTED =""",VCOMP,"""" W:$Y>55 #
	W !,"           CORRECT  =""",VCORR,"""" W:$Y>55 #
