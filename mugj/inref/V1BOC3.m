V1BOC3	;BINARY OPERATORS -3.3- (&!_);YS-TS,V1BOC,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1978
	S PASS=0,FAIL=0
	W !!,"V1BOC3: TEST OF BINARY OPERATORS -3.3- (_)" W:$Y>55 #
	W !!,"Concatenation (_)",!
160	W !,"I-160  expratoms are strlit"
	S ITEM="I-160.1  ""A""_""B""" S VCOMP="A"_"B",VCORR="AB" D EXAMINER
	S ITEM="I-160.2  ""#""_""%""" S VCOMP="#"_"%",VCORR="#%" D EXAMINER
	S ITEM="I-160.3  ""000""_""010""" S VCOMP="000"_"010",VCORR="000010" D EXAMINER
	S ITEM="I-160.4  ""_""""_""_""zxcv""" S VCOMP="_""_"_"zxcv",VCORR="_""_zxcv" D EXAMINER
	;
161	W !,"I-161  expratoms are numlit"
	S ITEM="I-161.1  2_3" S VCOMP=2_3,VCORR="23" D EXAMINER
	S ITEM="I-161.2  000.000_3.4" S VCOMP=000.000_3.4,VCORR="03.4" D EXAMINER
	S ITEM="I-161.3  3E1_-.5E1" S VCOMP=3E1_-.5E1,VCORR="30-5" D EXAMINER
	;
162	W !,"I-162  relation with unary operator"
	S ITEM="I-162.1  '0_''0" S VCOMP='0_''0,VCORR="10" D EXAMINER
	S ITEM="I-162.2  000.000_+""3.4E2""" S VCOMP=000.000_+"3.4E2",VCORR="0340" D EXAMINER
	;
163	W !,"I-163  more than one concatenation in one expr"
	S ITEM="I-163  ",V=""
	S VCOMP="THES"_"E TWO "_""""_"L"_"INES SH"_"OULD BE E"_""_"XACT"_"LY IDE"_"NTICAL."
	S VCORR="THESE TWO ""LINES SHOULD BE EXACTLY IDENTICAL." D EXAMINER
	;
164	W !,"I-164  expratoms are lvn"
	S A="A",B=2,C=03.40,D="2A",A(29)=029,B(0,20)="020ZX"
	S ITEM="I-164.1  A_B" S VCOMP=A_B,VCORR="A2" D EXAMINER
	S ITEM="I-164.2  B_C_""ABD""_D" S VCOMP=B_C_"ABD"_D,VCORR="23.4ABD2A" D EXAMINER
	S ITEM="I-164.3  C_D" S VCOMP=C_D,VCORR="3.42A" D EXAMINER
	S ITEM="I-164.4  A_-D" S VCOMP=A_-D,VCORR="A-2" D EXAMINER
	S ITEM="I-164.5  A(29)_B(0,20)" S VCOMP=A(29)_B(0,20),VCORR="29020ZX" D EXAMINER
	;
END	W !!,"END OF V1BOC3",!
	S ROUTINE="V1BOC3",TESTS=15,AUTO=15,VISUAL=0 D ^VREPORT
	K  Q
	;
EXAMINER	I VCORR=VCOMP S PASS=PASS+1 W !,"   PASS  ",ITEM W:$Y>55 # Q
	S FAIL=FAIL+1 W !,"** FAIL  ",ITEM W:$Y>55 #
	W !,"           COMPUTED =""",VCOMP,"""" W:$Y>55 #
	W !,"           CORRECT  =""",VCORR,"""" W:$Y>55 #
