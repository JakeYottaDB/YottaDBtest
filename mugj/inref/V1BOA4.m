V1BOA4	;BINARY OPERATORS -1.4- (+-*/#\);YS-TS,V1BOA,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1978
	S PASS=0,FAIL=0
	W !!,"V1BOA4: TEST OF BINARY OPERATORS -1.4- (ARITHMETIC: +-*/#\ )"
	W !!,"algebraic quotient",!
46	W !,"I-46  expratom=0"
	S ITEM="I-46.1  0/1",VCOMP=0/1,VCORR=0 D EXAMINER
	S ITEM="I-46.2  0/-6",VCOMP=0/-6,VCORR=0 D EXAMINER
	;
47	W !,"I-47  left expratom>0, right expratom>0"
	S ITEM="I-47.1  3/3",VCOMP=3/3,VCORR=1 D EXAMINER
	S ITEM="I-47.2  8/2",VCOMP=8/2,VCORR=4 D EXAMINER
	S ITEM="I-47.3  3/2",VCOMP=3/2,VCORR=1.5 D EXAMINER
	S ITEM="I-47.4  3/4",VCOMP=3/4,VCORR=.75 D EXAMINER
	S ITEM="I-47.5  10/4",VCOMP=10/4,VCORR=2.5 D EXAMINER
	S ITEM="I-47.6  .5/0.5",VCOMP=.5/0.5,VCORR=1 D EXAMINER
	S ITEM="I-47.7  4.1/0.01",VCOMP=4.1/0.01,VCORR=410 D EXAMINER
	;
48	W !,"I-48  left expratom>0, right expratom<0"
	S ITEM="I-48.1  11/-4",VCOMP=11/-4,VCORR=-2.75 D EXAMINER
	S ITEM="I-48.2  .3/-0.3",VCOMP=.3/-0.3,VCORR=-1 D EXAMINER
	S ITEM="I-48.3  016.1370/-01.630",VCOMP=016.1370/-01.630,VCORR=-9.9 D EXAMINER
	S ITEM="I-48.4  0.0618700/-026.90",VCOMP=0.0618700/-026.90,VCORR=-.0023 D EXAMINER
	;
49	W !,"I-49  left expratom<0, right expratom>0"
	S ITEM="I-49.1  -9/4",VCOMP=-9/4,VCORR=-2.25 D EXAMINER
	S ITEM="I-49.2  -597.5/25",VCOMP=-597.5/25,VCORR=-23.9 D EXAMINER
	S ITEM="I-49.3  -0.9191799/999.0",VCOMP=-0.9191799/999.0,VCORR=-.0009201 D EXAMINER
	;
50	W !,"I-50  left expratom<0, right expratom<0"
	S ITEM="I-50.1  -12/-4",VCOMP=-12/-4,VCORR=3 D EXAMINER
	S ITEM="I-50.2  -50.3/-0.25",VCOMP=-50.3/-0.25,VCORR=201.2 D EXAMINER
	S ITEM="I-50.3  -00.00404/-0.10100",VCOMP=-00.00404/-0.10100,VCORR=.04 D EXAMINER
	;
51	W !,"I-51  expratoms are numlit"
	S ITEM="I-51.1  1E1/1.60",VCOMP=1E1/1.60,VCORR=6.25 D EXAMINER
	S ITEM="I-51.2  64777779E-07/0.099E+3",VCOMP=64777779E-07/0.099E+3,VCORR=.0654321 D EXAMINER
	S ITEM="I-51.3  00023.E3/11.50000E-2",VCOMP=00023.E3/11.50000E-2,VCORR=200000 D EXAMINER
	S ITEM="I-51.4  .123E2/12300E-2",VCOMP=.123E2/12300E-2,VCORR=.1 D EXAMINER
	;
52	W !,"I-52  expratoms are strlit"
	S ITEM="I-52.1  ""AB2""/""2AB""",VCOMP="AB2"/"2AB",VCORR=0 D EXAMINER
	S ITEM="I-52.2  ""2A2B""/""2E2B""",VCOMP="2A2B"/"2E2B",VCORR=.01 D EXAMINER
	;
53	W !,"I-53  expratoms are lvn"
	S ITEM="I-53.1  unsubscripted lvn" S A="-.0",B="-00002E",VCOMP=A/B,VCORR=0 D EXAMINER
	S ITEM="I-53.2  subscripted lvn" S A(8)="98.34E+@3",B(9)=0.001,VCOMP=A(8)/B(9),VCORR=98340 D EXAMINER
	;
END	W !!,"END OF V1BOA4",!
	S ROUTINE="V1BOA4",TESTS=27,AUTO=27,VISUAL=0 D ^VREPORT
	K  Q
	;
EXAMINER	I VCORR=VCOMP S PASS=PASS+1 W !,"   PASS  ",ITEM W:$Y>55 # Q
	S FAIL=FAIL+1 W !,"** FAIL  ",ITEM W:$Y>55 #
	W !,"           COMPUTED =""",VCOMP,"""" W:$Y>55 #
	W !,"           CORRECT  =""",VCORR,"""" W:$Y>55 #
