V1PCC ;IW-YS-MM-TS,V1PC,MVTS V9.10;15/6/96;POST-CONDITIONALS -3-
 ;COPYRIGHT MUMPS SYSTEMS LABORATORY 1978-1996
 W !!,"138---V1PCC: Post conditionals -3-",!
716 S ^ABSN="11722",^ITEM="I-716  DO command ( command word )",^NEXT="717^V1PCC,V1FORA^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP="" I 1
 DO:0 1 S ^VCOMP=^VCOMP_$T_" " I 0 ;COMMENT
 D:1 10 S ^VCOMP=^VCOMP_$T_" "
 S ^VCORR="1 10 0 " D ^VEXAMINE
 ;
717 S ^ABSN="11723",^ITEM="I-717  DO command ( argument )",^NEXT="842^V1PCC,V1FORA^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP="" I 1
 DO ARG1:0 S ^VCOMP=^VCOMP_$T_" " I 0 ;COMMENT
 D ARG2:1 S ^VCOMP=^VCOMP_$T_" "
 S ^VCORR="1 ARG2 0 " D ^VEXAMINE
 ;
842 S ^ABSN="11724",^ITEM="I-842  DO with postconditionalized label list",^NEXT="843^V1PCC,V1FORA^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP="" S V1PC="$"
 D:V1PC="$" %0,%1:1,%2:$L(V1PC)=1,%3:$E(V1PC,2)="$" D:V1PC %01,%02:1,%03
 S ^VCORR="%0 Z0 %1 %2 " D ^VEXAMINE
 ;
843 S ^ABSN="11725",^ITEM="I-843  DO with postconditionalized label+intexpr list",^NEXT="844^V1PCC,V1FORA^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP="" K A
 D BYTE+2:$D(A),BYTE+1:'$D(A)
 S ^VCORR="BYTE+1 " D ^VEXAMINE
 ;
844 S ^ABSN="11726",^ITEM="I-844  DO with postconditionalized ^routineref list",^NEXT="845^V1PCC,V1FORA^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP=""
 S A=1,I=1 D:A ^V1PC1 S I=2 D ^V1PC1:A
 S I=3 D ^V1PC1:A=0 S I=4 D:A ^V1PC1:A,^V1PC1:'A,^V1PC1:I=4
 S ^VCORR="^V1PC11 ^V1PC12 ^V1PC14 ^V1PC14 " D ^VEXAMINE
 ;
845 S ^ABSN="11727",^ITEM="I-845  DO with postconditionalized label^routineref list",^NEXT="846^V1PCC,V1FORA^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP=""
 D OS^V1PC1:2=2=1,FILE^V1PC1:$J("",2)="  ",010^V1PC1:3=3=3
 S ^VCORR="OS FILE " D ^VEXAMINE
 ;
846 S ^ABSN="11728",^ITEM="I-846  DO with postconditionalized label+intexpr^routineref list",^NEXT="718^V1PCC,V1FORA^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP="" K ^V1PC1
 S ^V1PC1=10
 D OS+1^V1PC1:^V1PC1=1,OS+2^V1PC1:^V1PC1=10.0,OS+3^V1PC1:^V1PC1-5-5
 DO:^V1PC1=1 OS+4^V1PC1:1,OS+4^V1PC1:"ABC"="ABC"
 D:10=^V1PC1 ABCDEFGH+1^V1PC1:$F(0.234560,0)=0,ABCDEFGH+2^V1PC1:$D(^V1PC1(1))=1
 S X=13,Y=14,^V1PC1=1
 D O^V1PC1,O+1^V1PC1,O^V1PC1:X=15,Q^V1PC1:$D(X),A:0,R^V1PC1:$E("ABCDEFGH",1,3)="ABC",S^V1PC1:X=13=(Y=14)
 D:$D(X) T^V1PC1:^V1PC1=1,T+1^V1PC1,T^V1PC1:X=15,V^V1PC1
 D:$D(^V1PC1)=0 T^V1PC1:X=13,T+1^V1PC1,T^V1PC1:X=15,U^V1PC1
 S ^VCORR="OS+2 ABCDEFGH+1 OPQRSTUV" D ^VEXAMINE
 ;
718 W !,"I-718  KILL command"
 S ^ABSN="11729",^ITEM="I-718.1  Local",^NEXT="7182^V1PCC,V1FORA^VV1" D ^V1PRESET
 S ^VCOMP="" K X,Y
 S X=13,X(1)=1,X(1,2)=12,X(1,2,3)=123,X(1,2,1)=121,X(3)=3
 S Y=1,Y(2,1)=21 S ^VCOMP=^VCOMP_$D(Y)_" "
 KILL:X=13 Y S ^VCOMP=^VCOMP_$D(Y)_" "
 S ^VCOMP=^VCOMP_$D(X(1,2))_" "
 K:$D(X(1))=11 X(1,2) S ^VCOMP=^VCOMP_$D(X(1,2))_" "
 K:$D(X) X S ^VCOMP=^VCOMP_$D(X)
 S ^VCORR="11 0 11 0 0" D ^VEXAMINE
 ;
7182 S ^ABSN="11730",^ITEM="I-718.2  Global",^NEXT="V1FORA^VV1" D ^V1PRESET
 S ^VCOMP="" K X,Y,^V1
 S ^V1=1,^V1(1,2)=12,^V1(1,2,2)=122
 S ^VCOMP=^VCOMP_$D(^V1(1,2))_" "
 K:^V1(1,2,2)=122 ^V1(1,2) S ^VCOMP=^VCOMP_$D(^V1(1,2))_" "
 K:$L("ABD")>2 ^V1 S ^VCOMP=^VCOMP_$D(^V1)
 S ^VCORR="11 0 0" D ^VEXAMINE
 ;
END W !!,"End of 138---V1PCC",!
 K  K ^V,^V1PC1,^V1 Q
 ;
A S ^VCOMP=^VCOMP_"A " Q  ;846 (test corrected in V7.3;20/6/88)
%01 S ^VCOMP=^VCOMP_"%01 " D Z01 Q  ;842
%0 S ^VCOMP=^VCOMP_"%0 " D Z0 Q  ;842
Z01 S ^VCOMP=^VCOMP_"Z01 " Q  ;842
Z0 S ^VCOMP=^VCOMP_"Z0 " Q  ;842
%02 S ^VCOMP=^VCOMP_"%02 " Q  ;842
%03 S ^VCOMP=^VCOMP_"%03 " Q  ;842
%2 S ^VCOMP=^VCOMP_"%2 " Q  ;842
%1 S ^VCOMP=^VCOMP_"%1 " Q  ;842
%3 S ^VCOMP=^VCOMP_"%3 " Q  ;842
10 S ^VCOMP=^VCOMP_"10 " Q  ;716
ARG1 S ^VCOMP=^VCOMP_"ARG1 " Q  ;717
1 S ^VCOMP=^VCOMP_"1 " Q  ;716
ARG2 S ^VCOMP=^VCOMP_"ARG2 " Q  ;717
BYTE S ^VCOMP=^VCOMP_"BYTE " Q  ;843
 S ^VCOMP=^VCOMP_"BYTE+1 " Q  ;843
 S ^VCOMP=^VCOMP_"BYTE+2 " Q  ;843
 S ^VCOMP=^VCOMP_"BYTE+3 " Q  ;843
SUM S SUM=0 F I=1:1 S L=$T(+I) Q:L=""  F K=1:1:$L(L) S SUM=SUM+$A(L,K)
 Q
