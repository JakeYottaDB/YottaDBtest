V1FNE2	;FUNCTION $EXTRACT -2-;YS-TS,V1FN,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1978
	S PASS=0,FAIL=0
	W !!,"V1FNE2: TEST OF $EXTRACT FUNCTION -2-" W:$Y>55 #
	W !!,"$EXTRACT(expr1,intexpr2,intexpr3)",! W:$Y>55 #
281	W !,"I-281  intexpr2<intexpr3"
	S ITEM="I-281.1  intexpr3<$L(expr1)",VCOMP=$E("ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890",26,28) S VCORR="Z12" D EXAMINER
	;
	S ITEM="I-281.2  intexpr contains lvn"
	S VCOMP="" F I=1:4:36 S VCOMP=VCOMP_$E("ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890",I,I+1)
	S VCORR="ABEFIJMNQRUVYZ3478" D EXAMINER
	;
	S ITEM="I-281.3  intexpr3>$L(expr1)"
	S VCOMP=$E("ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890",1,256)
	S VCORR="ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890" D EXAMINER
	;
	S ITEM="I-281.4  intexpr2<0"
	S A="ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890",VCOMP=$E(A,-5,"5E.AC"),VCORR="ABCDE" D EXAMINER
	;
	S ITEM="I-281.5  intexpr2<0 and intexpr3>$L(expr1)",VCOMP=$E("ABCDEFGHIJ",-1,"12.99ABC") S VCORR="ABCDEFGHIJ" D EXAMINER
	;
	S ITEM="I-281.6  expr1 is function",VCOMP=$E($EXTRACT("ABCDEFGHIJ",3,8),-1,"3.99ABC") S VCORR="CDE" D EXAMINER
	;
282	W !,"I-282  intexpr2=intexpr3"
	S ITEM="I-282.1  intexpr=1" S VCOMP=$E("PQRSTUVWXYZ1234567890",1,1) S VCORR="P" D EXAMINER
	S ITEM="I-282.2  1<intexpr<$L(expr1)" S VCOMP=$E(-000723.89400E-01,3,3) S VCORR="2" D EXAMINER
	S ITEM="I-282.3  intexpr=$L(expr1)",VCOMP=$E("ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890",36,36),VCORR="0" D EXAMINER
	S ITEM="I-282.4  intexpr=0" S VCOMP=$E("ABCDEFGIJKL567890",0,0) S VCORR="" D EXAMINER
	S ITEM="I-282.5  intexpr<-1" S VCOMP=$E("ABC90",-1,-1) S VCORR="" D EXAMINER
	S ITEM="I-282.6  intexpr>$L(expr1)" S VCOMP=$E("ABC90",77,77) S VCORR="" D EXAMINER
	S ITEM="I-282.7  expr1 is empty string and intexpr=1" S VCOMP=$E("",1,1) S VCORR="" D EXAMINER
	;
283	W !,"I-283  intexpr2>intexpr3"
	S ITEM="I-283.1  intexpr3>0" S VCOMP=$E("PQRSTUVWXYZ1234567890",2,1) S VCORR="" D EXAMINER
	S ITEM="I-283.2  intexpr2>1 and intexpr3<-1",VCOMP=$EXTRACT("PQRSTUVWXYZ1234567890",+3,-4) S VCORR="" D EXAMINER
	;
284	W !,"I-284  intexpr3>$LENGTH(expr1)"
	S ITEM="I-284.1  intexpr2=1" S VCOMP=$E("PQRST",1,10) S VCORR="PQRST" D EXAMINER
	S ITEM="I-284.2  1<intexpr2<$L(expr1)" S VCOMP=$E("PQRST",3,10) S VCORR="RST" D EXAMINER
	S ITEM="I-284.3  expr1 contains unary operator",VCOMP=$E(-"0.38FHF","ABCD",10) S VCORR="-.38" D EXAMINER
	S ITEM="I-284.4  intexpr2<-1",VCOMP=$E("PQRST",-23,10),VCORR="PQRST" D EXAMINER
	;
285	W !,"I-285  intexpr3>intexpr2>$LENGTH(expr1)"
	S ITEM="I-285  " S VCOMP=$E(12+34,6,10) S VCORR="" D EXAMINER
	;
286	W !,"I-286  intexpr2>$LENGTH(expr1) and intexpr3<0"
	S ITEM="I-286  " S VCOMP=$E("PQRST",8,-10) S VCORR="" D EXAMINER
	;
END	W !!,"END OF V1FNE2",!
	S ROUTINE="V1FNE2",TESTS=21,AUTO=21,VISUAL=0 D ^VREPORT
	K  Q
	;
EXAMINER	I VCORR=VCOMP S PASS=PASS+1 W !,"   PASS  ",ITEM W:$Y>55 # Q
	S FAIL=FAIL+1 W !,"** FAIL  ",ITEM W:$Y>55 #
	W !,"           COMPUTED =""",VCOMP,"""" W:$Y>55 #
	W !,"           CORRECT  =""",VCORR,"""" W:$Y>55 #
