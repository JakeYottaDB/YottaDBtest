VV2PAT2	;PATTERN MATCH OPERATOR -2-;KO-TS,,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1984
	S PASS=0,FAIL=0
	W !!,"VV2PAT2: TEST OF PATTERN MATCH OPERATOR -2-",!
155	W !,"II-155  Indirection pattern"
	S ITEM="II-155  ",VCOMP=""
	S VCOMP="ABC123#$!"?@(".4AN2.N1.99999999PN")_("12.34"?2.N.P.2N)
	S VCORR="11" D EXAMINER
	;
156	W !,"II-156  Nesting of pattern"
	S ITEM="II-156  ",VCOMP=""
	S VCOMP="MUMPS"?@("VALIDATION"?@("TEST"?4A_"""VALIDATION""")_"A"_($P("ABC/DDD","/")?.N+3_"U"_"1""S"""))
	S VCORR="1" D EXAMINER
	;
157	W !,"II-157  expr is 255 characters"
	S ITEM="II-157  ",VCOMP="" S X="" F I=0:1:127 S X=X_$C(I)
	F I=0:1:126 S X=X_$C(I)
	S VCOMP=(X?1.255CNPA)_(X?.999CNP)_(X?255.999CNPAULCNPAUL)_(X'?255.999CNNPAUL)
	S VCORR="1010" D EXAMINER
	;
158	W !,"II-158  expr ? repcount strlit when strlit is empty string"
	S ITEM="II-158  ",VCOMP="",X=""
	S VCOMP=VCOMP_(X?."")_(X?.000"")_(X?00."")_(""?0.0"")_(""?0000"")_" "
	S VCOMP=VCOMP_(""?.11"")_(X?21."")_(X?1.100"")_(X?5"")_" "
	S VCOMP=VCOMP_("@"?."")_("A"?.11"")_("B"?1."")_("C"?1.100"")_("D"?999"")
	S VCORR="11111 1111 00000" D EXAMINER
	;
159	W !,"II-159  expr ? repcount strlit when expr is empty string"
	S ITEM="II-159  ",VCOMP="",X=""
	S VCOMP=X?."@"_(""?.100"A")_(""?1."C")_(""?0."C")_(X?2.9"D")_(X?0.99"D")_(X?1"E")_(X?0"E")
	S VCORR="11010101" D EXAMINER
	;
160	W !,"II-160  Lower case letter pattern code ""c"""
	S ITEM="II-160.1  repcount",VCOMP=""
	s C="" s C=C_$C(127) F I=0:1:31 s C=C_$c(I)
	S VCOMP=VCOMP_(C?.c)_(C?33c)_(C?33e)_(C?.33c)_(C?33.c)_(C?1.33c)_(C?32c)
	S VCORR="1111110" D EXAMINER
	;
	S ITEM="II-160.2  its mapping",VCOMP="" F I=0:1:127 S A=$C(I)?1c,VCOMP=VCOMP_A
	S VCORR="11111111111111111111111111111111000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000001" D EXAMINER
	;
	S ITEM="II-160.3  lvn?5c",VCOMP="" F I=0:1:127 S A=$C(I,I,I,I,I),A=A?5c,VCOMP=VCOMP_A
	D EXAMINER
	;
161	W !,"II-161  Lower case letter pattern code ""p"""
	S ITEM="II-161.1  repcount",VCOMP=""
	s P="" f I=32:1:126 s I=$s(I=48:58,I=65:91,I=97:123,1:I),P=P_$c(I)
	S VCOMP=VCOMP_(P?.p)_(P?33p)_(P?33e)_(P?.33p)_(P?33.p)_(P?1.33p)_(P?30p)
	S VCORR="1111110" D EXAMINER
	;
	S ITEM="II-161.2  its mapping",VCOMP="" F I=0:1:127 S A=$C(I)?1p,VCOMP=VCOMP_A
	S VCORR="00000000000000000000000000000000111111111111111100000000001111111000000000000000000000000001111110000000000000000000000000011110" D EXAMINER
	;
	S ITEM="II-161.3  lvn?5p"
	S VCOMP="" F I=0:1:127 S A=$C(I,I,I,I,I),A=A?5p,VCOMP=VCOMP_A
	D EXAMINER
	;
END	W !!,"END OF VV2PAT2",!
	S ROUTINE="VV2PAT2",TESTS=11,AUTO=11,VISUAL=0 D ^VREPORT
	K  Q
	;
EXAMINER	I VCORR=VCOMP S PASS=PASS+1 W !,"   PASS  ",ITEM W:$Y>55 # Q
	S FAIL=FAIL+1 W !,"** FAIL  ",ITEM W:$Y>55 #
	W !,"           COMPUTED =""",VCOMP,"""" W:$Y>55 #
	W !,"           CORRECT  =""",VCORR,"""" W:$Y>55 #
