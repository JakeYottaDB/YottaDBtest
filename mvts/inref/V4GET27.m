V4GET27 ;IW-KO-YS-TS,V4GET2,MVTS V9.10;15/6/96;PART-94
 ;COPYRIGHT MUMPS SYSTEMS LABORATORY 1994-1996
 ;
 W !!,"29---V4GET27:  $GET function  -7-"
 ;
1 S ^ABSN="40223",^ITEM="IV-223  expr is an empty string"
 S ^NEXT="2^V4GET27,V4GET28^V4GET2,V4NAME^VV4" D ^V4PRESET K  K ^VV
 S ^VCOMP=$g(^VV("UUU",1),"")
 S ^VCORR="" D ^VEXAMINE K ^VV
 ;
2 S ^ABSN="40224",^ITEM="IV-224  expr is an integer number"
 S ^NEXT="3^V4GET27,V4GET28^V4GET2,V4NAME^VV4" D ^V4PRESET K  k ^VV
 S ^VCOMP=$g(^VV("123.45"),-1E25)
 S ^VCORR="-10000000000000000000000000" D ^VEXAMINE K ^VV
 ;
3 S ^ABSN="40225",^ITEM="IV-225  expr is a number"
 S ^NEXT="4^V4GET27,V4GET28^V4GET2,V4NAME^VV4" D ^V4PRESET K  k ^VV
 s ^VV(1)="123"
 S ^VCOMP=$g(^VV,1E-25)
 S ^VCORR=".0000000000000000000000001" D ^VEXAMINE K ^VV
 ;
4 S ^ABSN="40226",^ITEM="IV-226  expr is a string"
 S ^NEXT="5^V4GET27,V4GET28^V4GET2,V4NAME^VV4" D ^V4PRESET K  k ^VV
 s X="" f N=0:1:31 s X=X_$c(N)
 s M=$c(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16)
 s M=M_$c(17,18,19,20,21,22,23,24,25,26,27,28,29,30,31)
 S ^VCOMP=$g(^VV($a(X),12),X)
 S ^VCORR=M D ^VEXAMINE K ^VV
 ;
5 S ^ABSN="40227",^ITEM="IV-227  expr is a string with maximum length"
 S ^NEXT="6^V4GET27,V4GET28^V4GET2,V4NAME^VV4" D ^V4PRESET K  K ^VV
 S X="" f I=0:1:254 S X=X_(I#10)
 S A1=$C(0,1,2,3,4,5,6,7,8,9),A="" F I=1:1:25 S A=A_A1
 S A=A_$C(0,1,2,3,4)
 S ^VCOMP=$G(^VV("1234.45"),$tr(A,$C(9,8,7,6,5,4,3,2,1,0),"9876543210"))
 S ^VCORR=X D ^VEXAMINE K ^VV
 ;
6 S ^ABSN="40228",^ITEM="IV-228  expr is a naked reference"
 S ^NEXT="7^V4GET27,V4GET28^V4GET2,V4NAME^VV4" D ^V4PRESET K  K ^VV
 s ^VV("A",1,2,3)="A123"
 s ^VV("B",1,2,3)="B123"
 s ^VV("B",1)="B1"
 S ^VV(1)=1
 S ^VCOMP=$g(^($d(^VV("A",1))),^(1,2,3))
 S ^VCORR="A123" D ^VEXAMINE K ^VV
 ;
7 S ^ABSN="40229",^ITEM="IV-229  expr is a lvn"
 S ^NEXT="8^V4GET27,V4GET28^V4GET2,V4NAME^VV4" D ^V4PRESET K  K ^VV
 S A="AA",A(3)=3,A(2,3)=23,A(2.1)=2.1
 S ^VCOMP=$GET(^VV(6/3),A(2,3))
 S ^VCORR="23" D ^VEXAMINE K ^VV
 ;
8 S ^ABSN="40230",^ITEM="IV-230  expr is a gvn"
 S ^NEXT="9^V4GET27,V4GET28^V4GET2,V4NAME^VV4" D ^V4PRESET K  K ^VV
 S A="B",B="C",C="D",^VV("D")="DD"
 S ^VV("B")="B",^VV(1,"B")=2,^VV(1,"A")=3
 S ^VCOMP=$G(^VV(@@A),^VV(1,"A"))_" "_^("B")
 S ^VCORR="DD 2" D ^VEXAMINE K ^VV
 ;
9 S ^ABSN="40231",^ITEM="IV-231  expr is a svn"
 S ^NEXT="V4GET28^V4GET2,V4NAME^VV4" D ^V4PRESET K  k ^VV
 I 1=2  ;
 S ^VCOMP=$g(^VV($h),$test)
 S ^VCORR="0" D ^VEXAMINE
 ;
END W !!,"End of 29 --- V4GET27",!
 K  Q
 ;
SUM S SUM=0 F I=1:1 S L=$T(+I) Q:L=""  F K=1:1:$L(L) S SUM=SUM+$A(L,K)
 Q
