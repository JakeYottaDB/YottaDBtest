V4ORD25 ;IW-KO-TS-YS,V4ORDER,MVTS V9.10;15/6/96;$ORDER
 ;
 ;COPYRIGHT MUMPS SYSTEMS LABORATORY 1984-1996
 ;
 W !!,"112---V4ORD25:  $ORDER(glvn,expr)  -5-"
 ;
1 S ^ABSN="40705",^ITEM="IV-705  expr is an integer number"
 S ^NEXT="2^V4ORD25,V4ORD26^V4ORDER,V4QUERY^VV4" D ^V4PRESET K
 S V(1,2,3,4,6)=10
 S V(1,2,3,4,1)=1
 S ^VCOMP=$o(V(1,2,3,4,5),-1)
 S ^VCORR="1" D ^VEXAMINE
 ;
2 S ^ABSN="40706",^ITEM="IV-706  expr is a number"
 S ^NEXT="3^V4ORD25,V4ORD26^V4ORDER,V4QUERY^VV4" D ^V4PRESET K  K ^V
 S ^V("ABC",123,"ABCD",123,"A")=""
 S ^VCOMP=$O(^V("ABC",123,"ABC"),0.001E+3)
 S ^VCORR="ABCD" D ^VEXAMINE K ^V
 ;
3 S ^ABSN="40707",^ITEM="IV-707  expr is a string"
 S ^NEXT="4^V4ORD25,V4ORD26^V4ORDER,V4QUERY^VV4" D ^V4PRESET K
 S V("A","B",1,"R","E")=1
 S ^VCOMP=$O(V("A","B",1,"x"),"-1")
 S ^VCORR="R" D ^VEXAMINE
 ;
4 S ^ABSN="40708",^ITEM="IV-708  expr is a naked reference"
 S ^NEXT="5^V4ORD25,V4ORD26^V4ORDER,V4QUERY^VV4" D ^V4PRESET K  K ^V
 S ^V(-234.344,2)=222,^V(-234.344,2,3)=1,^V(-234.344,"AAA")="a"
 S ^V(-234.344,3)="ERROR"
 S ^VCOMP=$O(^V(-234.344,23.44),^(2,3))_" "_^(3)
 S ^VCORR="AAA 1" D ^VEXAMINE K ^V
 ;
5 S ^ABSN="40709",^ITEM="IV-709  expr is a lvn"
 S ^NEXT="6^V4ORD25,V4ORD26^V4ORDER,V4QUERY^VV4" D ^V4PRESET K
 s A(1,2)=-1,V(-111,"a",-29.29)=11,V(-111,"a",-29.29,"A","b")=10
 S ^VCOMP=$o(V(-111,"a",-1),A(1,2))
 S ^VCORR="-29.29" D ^VEXAMINE
 ;
6 S ^ABSN="40710",^ITEM="IV-710  expr is a gvn"
 S ^NEXT="7^V4ORD25,V4ORD26^V4ORDER,V4QUERY^VV4" D ^V4PRESET K  k ^V
 S ^V(1,"a","b")="a",^V("A",2)=1,^V(1,2)="ERROR"
 S ^VCOMP=$o(^V(1,""),^V("A",2))_" "_^(2)
 S ^VCORR="2 1" D ^VEXAMINE k ^V
 ;
7 S ^ABSN="40711",^ITEM="IV-711  expr is a svn"
 S ^NEXT="V4ORD26^V4ORDER,V4QUERY^VV4" D ^V4PRESET K
 S V(1,0,1,0)=1,V(1,0,0,222)=2,V(1,0,0,111)=1 IF 1
 S ^VCOMP=$O(V(1,0,0,1),$TEST)
 S ^VCORR="111" D ^VEXAMINE
 ;
END W !!,"End of 112 --- V4ORD25",!
 K  Q
SUM S SUM=0 F I=1:1 S L=$T(+I) Q:L=""  F K=1:1:$L(L) S SUM=SUM+$A(L,K)
 Q
