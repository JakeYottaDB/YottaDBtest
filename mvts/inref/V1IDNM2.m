V1IDNM2 ;IW-KO-TS,V1IDNM,MVTS V9.10;15/6/96;NAME LEVEL INDIRECTION -2-
 ;COPYRIGHT MUMPS SYSTEMS LABORATORY 1978-1996
 W !!,"147---V1IDNM2: Name level indirection -2-"
 W !!,"SET command",!
497 S ^ABSN="11799",^ITEM="I-497  Indirection of the left side lvn",^NEXT="498^V1IDNM2,V1IDNM3^V1IDNM,V1IDGO^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP="" S A="B",@A=1 S ^VCOMP=^VCOMP_B_A
 S ^VCORR="1B" D ^VEXAMINE
 ;
498 S ^ABSN="11800",^ITEM="I-498  Indirection of the right side lvn",^NEXT="499^V1IDNM2,V1IDNM3^V1IDNM,V1IDGO^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP=""
 S A="C",B="D",CD=3,AB=@(A_B) S ^VCOMP=^VCOMP_AB_CD S ^VCORR="33" D ^VEXAMINE
 ;
499 S ^ABSN="11801",^ITEM="I-499  Indirection of the left side gvn",^NEXT="500^V1IDNM2,V1IDNM3^V1IDNM,V1IDGO^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP=""
 S ^V1A(100)="^V1B(2)",@^V1A(100)=100 S ^VCOMP=^VCOMP_^V1B(2)_^V1A(100)
 S ^VCORR="100^V1B(2)" D ^VEXAMINE
 ;
500 S ^ABSN="11802",^ITEM="I-500  Indirection of the right side gvn",^NEXT="501^V1IDNM2,V1IDNM3^V1IDNM,V1IDGO^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP="" S ^V1A(100)="^V1B(2)",^V1B(2)=300,^V1A(200)=@^V1A(100)
 S ^VCOMP=^VCOMP_^V1A(200)_^V1B(2)_^V1A(100)
 S ^VCORR="300300^V1B(2)" D ^VEXAMINE
 ;
501 S ^ABSN="11803",^ITEM="I-501  Indirection of lvn subscript",^NEXT="502^V1IDNM2,V1IDNM3^V1IDNM,V1IDGO^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP=""
 S B="B1",B1=2,C="C(D)",D=3,C(3)=4,A(10,20)=30
 S A(@B)=10,A(@B,@C)=20,A(1)=A(@B),A(3)=A(A(@B),A(@B,@C))
 S ^VCOMP=A(2)_A(2,4)_A(1)_A(3),^VCORR="10201030" D ^VEXAMINE
 ;
502 S ^ABSN="11804",^ITEM="I-502  Indirection of gvn subscript",^NEXT="503^V1IDNM2,V1IDNM3^V1IDNM,V1IDGO^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP=""
 S ^V1A(2)="^V1A(3)",^(3)=22,^(4)="^V1A(5)",^V1A(5)=55
 S A="@^V1A(2)",B="^V1A(C)",C=00002,^V1A(@A)=100,^V1A(@^(4))=200
 S ^VCOMP=^VCOMP_^V1A(22)_^V1A(55),^VCORR="100200" D ^VEXAMINE
 ;
503 S ^ABSN="11805",^ITEM="I-503  Value of indirection contains function",^NEXT="504^V1IDNM2,V1IDNM3^V1IDNM,V1IDGO^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP=""
 S A="@$E(""ABCDEF"",3)",B="@$E(""ABCDEF"",4)",D=40
 S @A=@B S @$P("BS;BB;FF;IO",";",2)=50
 S ^VCOMP=^VCOMP_C_BB,^VCORR="4050" D ^VEXAMINE
 ;
504 S ^ABSN="11806",^ITEM="I-504  Value of indirection is gvn",^NEXT="505^V1IDNM2,V1IDNM3^V1IDNM,V1IDGO^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP="" K ^V1A
 S A="^V1A(B)",B=2,C="^(2,D)",D=10,@A="ABC",@C=20,%Z="^V1A(1)",^V1A(1)="###",%Z=@%Z
 S ^VCOMP=^VCOMP_^V1A(2)_^V1A(2,10)_%Z
 S ^VCORR="ABC20###" D ^VEXAMINE
 ;
505 S ^ABSN="11807",^ITEM="I-505  Value of indirection is lvn",^NEXT="506^V1IDNM2,V1IDNM3^V1IDNM,V1IDGO^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP=""
 S A="A(A(1))",A(1)=2,A(2)="20:10:40",B="@A(AA)",AA=11,A(11)="D"
 S @B=@A+5+@A,@("AB"_(1+2))=" STOP"
 S ^VCOMP=^VCOMP_D_AB3,^VCORR="45 STOP" D ^VEXAMINE
 ;
506 S ^ABSN="11808",^ITEM="I-506  Value of indirection contains numeric literal",^NEXT="507^V1IDNM2,V1IDNM3^V1IDNM,V1IDGO^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP=""
 S @("%"_0023.00)="%23",@($E("ABC",2)_(1.2E2))="B120"
 S ^VCOMP=%23_" "_B120,^VCORR="%23 B120" D ^VEXAMINE
 ;
507 S ^ABSN="11809",^ITEM="I-507  2 levels of indirection",^NEXT="508^V1IDNM2,V1IDNM3^V1IDNM,V1IDGO^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP=""
 S A1="@B2",B2="C3(2)",A="B",B="C",C=4,@A1=@@A*2
 S ^VCOMP=^VCOMP_C3(2)_" "_A_B_C,^VCORR="8 BC4" D ^VEXAMINE
 ;
508 S ^ABSN="11810",^ITEM="I-508  3 levels of indirection",^NEXT="V1IDNM3^V1IDNM,V1IDGO^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP=""
 S A(1)="@A(2)",A(2)="@A(C)",A(3)="A(4)",A(4)="A(5)",C=3
 S B(1)="B(2)",B(2)="B(C)",B(3)="B(4)",B(4)="B(5)"
 S @A(1)=@@@B(1),@@@B(1)=@A(1)
 S ^VCOMP=^VCOMP_A(4)_" "_B(4),^VCORR="B(5) B(5)" D ^VEXAMINE
 ;
END W !!,"End of 147---V1IDNM2",!
 K  K ^V1A,^V1B Q
SUM S SUM=0 F I=1:1 S L=$T(+I) Q:L=""  F K=1:1:$L(L) S SUM=SUM+$A(L,K)
 Q
