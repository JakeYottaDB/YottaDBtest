V3NEWML1 ;IW-KO-YS-TS,V3NEW,MVTS V9.10;15/6/96;
 ;COPYRIGHT MUMPS SYSTEMS LABORATORY 1990-1996
 W !!,"109---V3NEWML1: NEW -39-"
 W !!,"length of value is 255"
 ;
83 S ^ABSN="30990",^ITEM="III-0990  $D(lvn)=0"
 S ^NEXT="84^V3NEWML1,V3NEWML2^V3NEW,V3ALDO^VV3" D ^V3PRESET K
 S ^VCOMP="" D SET
 D NEWL1
 D CHECK
 S ^VCORR="11 1 1 0 10 1 0 0 1111#0 0 1 0 10 1 0 0 11#"
 D ^VEXAMINE K ^VVA,^VVB,^VVC,^VVD
 ;
84 S ^ABSN="30991",^ITEM="III-0991  $D(lvn)=1"
 S ^NEXT="^V3NEWML2,V3NEWXA^V3NEW,V3ALDO^VV3" D ^V3PRESET K
 S ^VCOMP="" D SET
 S A=^VVA,B=^VVB,C=^VVC
 D NEWL2,CHECK
 S ^VCORR="11 1 1 0 10 1 0 0 0000#11 1 1 0 1 0 0 0 0011#"
 D ^VEXAMINE K ^VVA,^VVB,^VVC,^VVD
 ;
END W !!,"End of 109 --- V3NEWML1",!
 K  K ^VVA,^VVB,^VVC,^VVD Q
 ;
NEWL1 ;
 NEW A
 S A=^VVA,A(2)=^VVA,B=^VVB,C(2)=^VVC
 D CHECK
 Q
 ;
NEWL2 ;
 NEW B,C
 S A=2,A(2)=2,B=2,C(2)=2
 D CHECK
 QUIT
 ;
CHECK S ^VCOMP=^VCOMP_$D(A)_" "_$D(A(2))_" "
 S ^VCOMP=^VCOMP_$D(B)_" "_$D(B(2))_" "
 S ^VCOMP=^VCOMP_$D(C)_" "_$D(C(2))_" "
 S ^VCOMP=^VCOMP_$D(D)_" "_$D(D(2))_" "
 I $D(A)#10=1    S ^VCOMP=^VCOMP_(A=^VVA)
 I $D(A(2))#10=1 S ^VCOMP=^VCOMP_(A(2)=^VVA)
 I $D(B)#10=1    S ^VCOMP=^VCOMP_(B=^VVB)
 I $D(B(2))#10=1 S ^VCOMP=^VCOMP_(B(2)=^VVB)
 I $D(C)#10=1    S ^VCOMP=^VCOMP_(C=^VVC)
 I $D(C(2))#10=1 S ^VCOMP=^VCOMP_(C(2)=^VVC)
 I $D(D)#10=1    S ^VCOMP=^VCOMP_(D=^VVD)
 I $D(D(2))#10=1 S ^VCOMP=^VCOMP_(D(2)=^VVD)
 S ^VCOMP=^VCOMP_"#"
 Q
 ;
SET S A="" F I=1:1:255 S A=A_"A"
 S ^VVA=A K A
 S B="" F I=1:1:255 S B=B_"B"
 S ^VVB=B K B
 S C="" F I=1:1:255 S C=C_"C"
 S ^VVC=C K C
 S D="" F I=1:1:255 S D=D_"D"
 S ^VVD=D K D
 Q
 ;
SUM S SUM=0 F I=1:1 S L=$T(+I) Q:L=""  F K=1:1:$L(L) S SUM=SUM+$A(L,K)
 Q
