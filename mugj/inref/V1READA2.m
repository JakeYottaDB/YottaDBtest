V1READA2	;READ COMMAND -2-;KO-TS,V1READA,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1978
	S PASS=0,FAIL=0
	W !!,"V1READA2: TEST READ COMMAND -2-",!
TEST2	W !!,"TEST OF 'READ *lvn' :",!
757	W !,"I-757  READ *lvn"
	S ITEM="I-757.1  'A'",VCOMP="" W:$Y>50 #
	W !!,"CONFIRM THAT THE INPUT IS TERMINATED BY THE ENTRY OF ONE CHARACTER."
	W !,"IF THE MAPPING  BETWEEN THE INPUT CHARACTERS AND THE INTEGER VALUE"
	W !,"GIVEN TO LVN IS DEPENDENT ON ASCII TABLE, TYPE ENTRY KEY,"
	R !,"OTHERWISE TYPE ANOTHER KEY : ",X
	R !!,"TEST I-757.1: TYPE 'A'",*%
	I X="",%=65 S VCOMP=" ... PASSED"
	I X="",%'=65 S VCOMP=" ... FAILED"
	I X'="",%'?1N.N S VCOMP=" ... FAILED"
	I X'="",%?1N.N W !,"THE INTEGER VALUE GIVEN TO LVN IS ",% S VCOMP=" ... PASSED"
	S VCORR=" ... PASSED" D EXAMINER
	;
	S ITEM="I-757.2  <ENTER>",VCOMP=""
	READ !!,"TEST I-757.2: TYPE THE <ENTRY> KEY",*%2345678
	I X="",%2345678=13 S VCOMP=" ... PASSED"
	I X="",%2345678'=13 S VCOMP=" ... FAILED"
	I X'="",%2345678?1N.N W !,"THE INTEGER VALUE GIVEN TO LVN IS ",%2345678 S VCOMP=" ... PASSED"
	I X'="",%2345678'?1N.N S VCOMP=" ... FAILED"
	S VCORR=" ... PASSED" D EXAMINER
	;
758	W !!,"I-758  READ *lvn,*lvn,*lvn"
	S ITEM="I-758  ",VCOMP=""
	R !!,"TEST I-758: TYPE '1','!','Z' IN THIS SEQUENCE >",*ABCDEFGH,*%234567A,*Z
	I X="",ABCDEFGH=49,%234567A=33,Z=90 S VCOMP=" ... PASSED"
	I X="",(ABCDEFGH'=49)!(%234567A'=33)!(Z'=90) S VCOMP=" ... FAILED"
	I X'="" W !,"THE INTEGER VALUE GIVEN TO LVN IS ",ABCDEFGH," , "
	I  W %234567A," AND ",Z S VCOMP=" ... PASSED"
	I X'="",(ABCDEFGH'?1N.N)!(%234567A'?1N.N)!(Z'?1N.N) S VCOMP=" ... FAILED"
	S VCORR=" ... PASSED" D EXAMINER
	;
759	W !!,"I-759  Read into subscripted variable"
	S ITEM="I-759.1  READ lvn" K MUMPS
	R !!,"TEST I-759.1: TYPE 'MUMPS' AND <ENTER> KEY",!,MUMPS(100) S VCOMP=MUMPS(100)
	S VCORR="MUMPS" D EXAMINER
	;
	S ITEM="I-759.2  READ *lvn" K MUMPS
	R !!,"TEST I-759.2: TYPE 'M'",!,*MUMPS(100,1) S VCOMP=MUMPS(100,1)
	S VCORR=77 D EXAMINER
	;
END	W !!,"END OF V1READA2",!
	S ROUTINE="V1READA2",TESTS=5,AUTO=5,VISUAL=0 D ^VREPORT
	K  Q
	;
EXAMINER	I VCORR=VCOMP S PASS=PASS+1 W !,"   PASS  ",ITEM W:$Y>55 # Q
	S FAIL=FAIL+1 W !,"** FAIL  ",ITEM W:$Y>55 #
	W !,"           COMPUTED =""",VCOMP,"""" W:$Y>55 #
	W !,"           CORRECT  =""",VCORR,"""" W:$Y>55 #
	Q
