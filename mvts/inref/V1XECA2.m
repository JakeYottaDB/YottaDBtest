V1XECA2 ;IW-YS-TS,V1XECA,MVTS V9.10;15/6/96;XECUTE COMMAND -1.2-
 ;COPYRIGHT MUMPS SYSTEMS LABORATORY 1978-1996
 W !!,"160---V1XECA2: Xecute command -1.2-",!
810 W !,"I-810  Argument level indirection"
8101 S ^ABSN="11901",^ITEM="I-810.1  1 level",^NEXT="8102^V1XECA2,V1XECB^VV1" D ^V1PRESET
 S VCOMP="",X="Y",Y="S A=1 S VCOMP=A" X @X
 S ^VCOMP=VCOMP,^VCORR="1" D ^VEXAMINE
 ;
8102 S ^ABSN="11902",^ITEM="I-810.2  2 level",^NEXT="8103^V1XECA2,V1XECB^VV1" D ^V1PRESET
 S VCOMP="" S X="Y,Z",Y="S B=123,A=""B""",Z="S VCOMP=@A ;comment" X @X
 S ^VCOMP=VCOMP,^VCORR=123 D ^VEXAMINE
 ;
8103 S ^ABSN="11903",^ITEM="I-810.3  Value of indirection contains postcondition",^NEXT="811^V1XECA2,V1XECB^VV1" D ^V1PRESET
 S VCOMP="" S P=1,X="W:P=0,Y:P=1,Z",Y="S A=3",Z="S VCOMP=VCOMP_A",W="S VCOMP=1" X @X
 S ^VCOMP=VCOMP,^VCORR="3" D ^VEXAMINE
 ;
811 W !,"I-811  GOTO in XECUTE"
8111 S ^ABSN="11904",^ITEM="I-811.1  Local branching",^NEXT="8112^V1XECA2,V1XECB^VV1" D ^V1PRESET
 S VCOMP="" X "G B","S VCOMP=VCOMP_7"
 S ^VCOMP=VCOMP,^VCORR="67" D ^VEXAMINE
8112 S ^ABSN="11905",^ITEM="I-811.2  Overlay with external routine",^NEXT="812^V1XECA2,V1XECB^VV1" D ^V1PRESET
 S VCOMP="" X "S VCOMP=VCOMP_10 G ^V1XECAE S VCOMP=VCOMP_""ERROR 2 ""","S VCOMP=VCOMP_12"
 S ^VCOMP=VCOMP,^VCORR="101112" D ^VEXAMINE
 ;
812 W !,"I-812  FOR in XECUTE"
 S ^ABSN="11906",^ITEM="I-812  FOR in XECUTE",^NEXT="813^V1XECA2,V1XECB^VV1" D ^V1PRESET
 S VCOMP="" X "F I=1:1:3 S VCOMP=VCOMP_I","S VCOMP=VCOMP_4"
 X "F I=5:1:7 D C","S VCOMP=VCOMP_8"
 X "F I=9:1:11 G E","S VCOMP=VCOMP_10"
 X "S VCOMP=VCOMP_11 F I=12:1:14 S VCOMP=VCOMP_I Q:I>12","S VCOMP=VCOMP_14"
 S ^VCOMP=VCOMP,^VCORR="1234567891011121314" D ^VEXAMINE
 ;
813 W !,"I-813  DO in XECUTE"
8131 S ^ABSN="11907",^ITEM="I-813.1  Local branching",^NEXT="8132^V1XECA2,V1XECB^VV1" D ^V1PRESET
 S VCOMP="" X "D A","S VCOMP=VCOMP_5"
 S ^VCOMP=VCOMP,^VCORR="45" D ^VEXAMINE
8132 S ^ABSN="11908",^ITEM="I-813.2  Call external routine",^NEXT="8133^V1XECA2,V1XECB^VV1" D ^V1PRESET
 S VCOMP="" X "D 13^V1XECAE","S VCOMP=VCOMP_14"
 S ^VCOMP=VCOMP,^VCORR="1314" D ^VEXAMINE
8133 S ^ABSN="11909",^ITEM="I-813.3  Call external and local",^NEXT="814^V1XECA2,V1XECB^VV1" D ^V1PRESET
 S VCOMP=""
 S A="D A,EXTERN^V1XECAE S VCOMP=VCOMP_""1 "" D B,13^V1XECAE,A"
 X A S ^VCOMP=VCOMP,^VCORR="4EX 1 6134" D ^VEXAMINE
 ;
814 W !,"I-814  QUIT in XECUTE"
 S ^ABSN="11910",^ITEM="I-814  QUIT in XECUTE",^NEXT="815^V1XECA2,V1XECB^VV1" D ^V1PRESET
 S VCOMP=""
 X "S VCOMP=8 Q  S VCOMP=VCOMP_""ERROR 1 ""","S VCOMP=VCOMP_9"
 S ^VCOMP=VCOMP,^VCORR="89" D ^VEXAMINE
 ;
815 W !,"I-815  Nesting of XECUTE"
8151 S ^ABSN="11911",^ITEM="I-815.1  2 nesting levels",^NEXT="8152^V1XECA2,V1XECB^VV1" D ^V1PRESET
 S VCOMP="" X "X ""S A=1""","S VCOMP=A"
 S ^VCOMP=VCOMP,^VCORR="1" D ^VEXAMINE
 ;
8152 S ^ABSN="11912",^ITEM="I-815.2  3 nesting levels",^NEXT="8153^V1XECA2,V1XECB^VV1" D ^V1PRESET
 S ^VCOMP=""
 X "X ""X """"S A=2"""""",""S ^VCOMP=A"""
 S ^VCORR="2" D ^VEXAMINE
 ;
8153 S ^ABSN="11913",^ITEM="I-815.3  3 nesting levels another",^NEXT="V1XECB^VV1" D ^V1PRESET
 S VCOMP=""
 S X(1)="S A=3",X(2)="S VCOMP=VCOMP_A ;comment",X(3)="X X(2)",X(4)="X X(1),X(3)" X X(4) S ^VCOMP=VCOMP,^VCORR="3" D ^VEXAMINE
 ;
END W !!,"End of 160---V1XECA2",!
 K  Q
SUM S SUM=0 F I=1:1 S L=$T(+I) Q:L=""  F K=1:1:$L(L) S SUM=SUM+$A(L,K)
 Q
 ;
A S VCOMP=VCOMP_4 Q
B S VCOMP=VCOMP_6 Q
C S VCOMP=VCOMP_I Q
D S VCOMP=VCOMP_I Q
E S VCOMP=VCOMP_I
