VSR ;IW-KO-TS,VV1/VV2/VV3/VV4,MVTS V9.10;15/7/96;UTILITY
 ;COPYRIGHT MUMPS SYSTEMS LABORATORY 1984-1996
 ;
START ;
 I $D(^VENVIRON("INTEGRITY"))=0 D ^VINT9
 I ^VENVIRON("INTEGRITY")="OK" G START1
 I ^VENVIRON("INTEGRITY")="NOT OK" G START1
 D ^VINT9
START1 ;
 I $D(^VENVIRON("COMPLETE"))=1 D EDIT^VENVIRON
 I $D(^VENVIRON("COMPLETE"))=0 D ^VENVIRON Q
 S SAVEIO=$I
 I $D(^VENVIRON("#3 OPEN"))=0 G SEQ
 I ^VENVIRON("#3 OPEN")="" G SEQ ;fied V9.02
 O ^VENVIRON("#3 OPEN") U ^VENVIRON("#3 USE")
 D OUT
 I SAVEIO'=^VENVIRON("#3 USE") C ^VENVIRON("#3 CLOSE")
SEQ I $D(^VENVIRON("#4 OPEN"))=0 G EXIT
 I ^VENVIRON("#4 OPEN")="" G EXIT
 I ^VENVIRON("#4 OPEN")="Ignore" G EXIT
 IF ^VENVIRON("#4 OPEN")?1.N1":".E G SEQ1
 O ^VENVIRON("#4 OPEN") U ^VENVIRON("#4 USE")
 D OUT
 I SAVEIO'=^VENVIRON("#4 USE") C ^VENVIRON("#4 CLOSE")
EXIT U SAVEIO
 Q
SEQ1 O @^VENVIRON("#4 OPEN") U @^VENVIRON("#4 USE")
 D OUT
 I SAVEIO'=^VENVIRON("#4 USE") C @^VENVIRON("#4 CLOSE")
 U SAVEIO Q
 ;
OUT ;
 W #
 W "  VALIDATION SUMMARY REPORT FOR MVS V9.10 (PART-77,-84,-90,-94,-94TP)",!
 D %DATE^VSRE,%TIME^VSRE
 W "                                                         ",%DT,"  ",%T,! K %DT,%T
 S FAILURE=0,TPASS=0
 D TBL^VENVIRON,WITHD,VV1,VV2,VV3,VV4,^VSRE
 Q
 ;
WITHD ;
 W !!!,"              2)  Withdrawn or Suppressed Tests (*WITHDR)"
 D LIN
 D ^VSRWT
 D LIN W !!
 Q
 ;
 ;
VV1 ;
 S ^VREPORT="Part-77"
 S ^VREPORT("Part-77",12004)="*WITHDR"
 S ^VREPORT("Part-77",12141)="*WITHDR"
 S PASS=0,PASSO=0,FAIL=0,FAILO=0,ABORT=0,SKIP=0
 W !!,"   3)  PART-77   Valid Tests: 2105    Withdrawn or Suppressed Tests: 51"
 D LIN W !
 S T=1,COL=0
 F ABSN=10001:1:12156 D VSR1
 D LIN
 W !,"PART-77     PASS  PASSO         FAIL  FAILO        ABORTED   SKIP"
 W !,"                  BY OPERATOR         BY OPERATOR  "
 W !,"SUBTOTAL    ",$J(PASS,4),$J(PASSO,4+3),$J(FAIL,4+9),$J(FAILO,4+3),$J(ABORT,4+11),$J(SKIP,4+3)
 S FAILURE("Part-77")=FAIL+FAILO+ABORT+SKIP,FAILURE=FAILURE+FAILURE("Part-77"),PASS("Part-77")=PASS+PASSO,TPASS=TPASS+PASS+PASSO
 D LIN W !
 ;
 S ^VREPORT="Part-77"
 S PASS=0,FAIL=0,ABORT=0,SKIP=0
 W !!,"            4)  PART-77    Optional Tests: 8"
 D LIN W !
 S T=1,COL=0
 F OPT=1:1:8 S ABSN="OPT0"_OPT D VSR1
 D LIN
 W !,"PART-77-OPT    PASS       FAIL     ABORTED      SKIP"
 W !,"SUBTOTAL       ",$J(PASS,4),"       ",$J(FAIL,4),"        ",$J(ABORT,4),"      ",$J(SKIP,4)
 D LIN W !
 Q
 ;
VV2 ;
 S ^VREPORT="Part-84"
 S PASS=0,PASSO=0,FAIL=0,FAILO=0,ABORT=0,SKIP=0
 W !!,"   5)  PART-84   Valid Tests: 200    Withdrawn or Suppressed Tests: 24"
 D LIN W !
 S T=1,COL=0
 F ABSN=20001:1:20224 D VSR1
 D LIN
 W !,"PART-84     PASS  PASSO         FAIL  FAILO        ABORTED   SKIP"
 W !,"                  BY OPERATOR         BY OPERATOR  "
 W !,"SUBTOTAL    ",$J(PASS,4),$J(PASSO,4+3),$J(FAIL,4+9),$J(FAILO,4+3),$J(ABORT,4+11),$J(SKIP,4+3)
 S FAILURE("Part-84")=FAIL+FAILO+ABORT+SKIP,FAILURE=FAILURE+FAILURE("Part-84"),PASS("Part-84")=PASS+PASSO,TPASS=TPASS+PASS+PASSO
 D LIN W !
 Q
 ;
VV3 ;
 S ^VREPORT="Part-90"
 S PASS=0,PASSO=0,FAIL=0,FAILO=0,ABORT=0,SKIP=0
 W !!," 6)  PART-90    Valid Tests: 1102    Withdrawn or Suppressed Tests: 6"
 D LIN W !
 S T=1,COL=0
 F ABSN=30001:1:31108 D VSR1
 D LIN
 W !,"PART-90     PASS  PASSO         FAIL  FAILO        ABORTED   SKIP"
 W !,"                  BY OPERATOR         BY OPERATOR  "
 W !,"TOTAL       ",$J(PASS,4),$J(PASSO,4+3),$J(FAIL,4+9),$J(FAILO,4+3),$J(ABORT,4+11),$J(SKIP,4+3)
 S FAILURE("Part-90")=FAIL+FAILO+ABORT+SKIP,FAILURE=FAILURE+FAILURE("Part-90"),PASS("Part-90")=PASS+PASSO,TPASS=TPASS+PASS+PASSO
 D LIN W !
 Q
 ;
VV4 ;
 S ^VREPORT="Part-94"
 S PASS=0,PASSO=0,FAIL=0,FAILO=0,ABORT=0,SKIP=0
 W !!," 7)  PART-94(-94TP)    Valid Tests: 911    Withdrawn or Suppressed Tests: 12"
 D LIN W !
 S T=1,COL=0
 F ABSN=40001:1:40923 D VSR1
 D LIN
 W !,"PART-94     PASS  PASSO         FAIL  FAILO        ABORTED   SKIP"
 W !,"                  BY OPERATOR         BY OPERATOR  "
 W !,"TOTAL       ",$J(PASS,4),$J(PASSO,4+3),$J(FAIL,4+9),$J(FAILO,4+3),$J(ABORT,4+11),$J(SKIP,4+3)
 S FAILURE("Part-94")=FAIL+FAILO+ABORT+SKIP,FAILURE=FAILURE+FAILURE("Part-94"),PASS("Part-94")=PASS+PASSO,TPASS=TPASS+PASS+PASSO
 D LIN W !
 Q
 ;
VSR1 W ABSN," " S COL=COL+$L(ABSN)+1
 I $D(^VREPORT(^VREPORT,ABSN))#10=0 S STAT="*SKIP*"
 E  S STAT=^VREPORT(^VREPORT,ABSN)
 W STAT S COL=COL+$L(STAT)
 I STAT["PASS " S PASS=PASS+1
 I STAT["PASSO" S PASSO=PASSO+1
 I STAT["FAIL*" S FAIL=FAIL+1
 I STAT["FAILO" S FAILO=FAILO+1
 I STAT["ABORT" S ABORT=ABORT+1
 I STAT["SKIP" S SKIP=SKIP+1
 I T=5 S T=1,COL=0 W ! Q
 W $J("",T*16-COL) S COL=COL+$L($J("",T*16-COL)) S T=T+1
 Q
 ;
 ;
LIN W:$X !
 W "============================================================================" Q
SUM S SUM=0 F I=1:1 S L=$T(+I) Q:L=""  F K=1:1:$L(L) S SUM=SUM+$A(L,K)
 Q
