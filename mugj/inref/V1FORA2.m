V1FORA2	;FOR COMMAND -1.2-;YS-TS,V1FORA,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1978
	S PASS=0,FAIL=0
	W !!,"V1FORA2: TEST OF FOR COMMAND -1.2-",!
345	W !,"I-345  5 levels of FOR nesting"
	S ITEM="I-345  ",VCOMP="" F I=0:1:1 F J=0:1:1 F K=0:1:1 F L=0:1:1 F M=0:1:1 S VCOMP=VCOMP_I_J_K_L_M_" "
	S VCOMP=VCOMP_" "
	S VCORR="00000 00001 00010 00011 00100 00101 00110 00111 01000 01001 01010 01011 01100 01101 01110 01111 10000 10001 10010 10011 10100 10101 10110 10111 11000 11001 11010 11011 11100 11101 11110 11111  "
	D EXAMINER
	;
346	W !,"I-346  GOTO command in FOR scope"
	S ITEM="I-346  ",VCOMP="" F I=1:1:10 S VCOMP=VCOMP_I G G346
	S VCOMP=VCOMP_"!!"
G346	S VCOMP=VCOMP_I,VCORR="11" D EXAMINER
	;
347	W !,"I-347  QUIT command in FOR scope"
	S ITEM="I-347.1  QUIT without postcondition",VCOMP="" F I=1:1:1000 S VCOMP=VCOMP_I I I=4 S VCOMP=VCOMP_I QUIT
	S VCOMP=VCOMP_I,VCORR="123444" D EXAMINER
	;
	S ITEM="I-347.2  QUIT with postcondition",VCOMP=""
	S J=0 F I=0:0:0 Q:J>10  S J=J+1 S:I>0 I=1+I S:I<0 I=1 S:'I I=-1 S VCOMP=VCOMP_I
	S VCOMP=VCOMP_I_J,VCORR="-1112" D EXAMINER
	;
348	W !,"I-348  XECUTE command in FOR scope"
	S ITEM="I-348  ",VCOMP="" F I=1:2:6 XECUTE "S VCOMP=VCOMP_I"
	S VCOMP=VCOMP_I,VCORR="1355" D EXAMINER
	;
349	W !,"I-349  numexpr is string literal"
	S ITEM="I-349  ",VCOMP="" F I=".4E1E2":"-2.0.^^^":"-5EAZXZZ" S VCOMP=VCOMP_I
	S VCOMP=VCOMP_I,VCORR="420-2-4-4" D EXAMINER
	;
	W !!,"FOR lvn=numexpr1:numexpr2",!
350	W !,"I-350  numexpr1>0 and numexpr2>0"
	S ITEM="I-350  ",VCOMP="" S I=0 F I=1:1 S VCOMP=VCOMP_I I I=5 G G350
	S VCOMP=VCOMP_"##"
G350	S VCOMP=VCOMP_I,VCORR="123455" D EXAMINER
	;
351	W !,"I-351  numexpr1>0 and numexpr2<0"
	S ITEM="I-351  ",VCOMP="" S I=0 F I=1:-1 S VCOMP=VCOMP_I I I=-3 G G351
	S VCOMP=VCOMP_"##"
G351	S VCOMP=VCOMP_I,VCORR="10-1-2-3-3" D EXAMINER
	;
352	W !,"I-352  numexpr1<0 and numexpr2>0"
	S ITEM="I-352  ",VCOMP="" S I=0 F I=-2:1 S VCOMP=VCOMP_I I I=3 S VCOMP=VCOMP_I G G352
	S VCOMP=VCOMP_"##"
G352	S VCOMP=VCOMP_I,VCORR="-2-1012333" D EXAMINER
	;
353	W !,"I-353  numexpr1<0 and numexpr2<0"
	S ITEM="I-353  ",VCOMP="" S I=0 F I=-2:-1 S VCOMP=VCOMP_I I I=-5 S VCOMP=VCOMP_I G G353
	S VCOMP=VCOMP_"##"
G353	S VCOMP=VCOMP_I,VCORR="-2-3-4-5-5-5" D EXAMINER
	;
354	W !,"I-354  numexpr2=0"
	S ITEM="I-354  ",VCOMP="" S I=0 F I=-2:0 S VCOMP=VCOMP_I,I=I+1 I I=3 Q
	S VCOMP=VCOMP_I,VCORR="-2-10123" D EXAMINER
	;
END	W !!,"END OF V1FORA2",!
	S ROUTINE="V1FORA2",TESTS=11,AUTO=11,VISUAL=0 D ^VREPORT
	K  Q
	;
EXAMINER	I VCORR=VCOMP S PASS=PASS+1 W !,"   PASS  ",ITEM W:$Y>55 # Q
	S FAIL=FAIL+1 W !,"** FAIL  ",ITEM W:$Y>55 #
	W !,"           COMPUTED =""",VCOMP,"""" W:$Y>55 #
	W !,"           CORRECT  =""",VCORR,"""" W:$Y>55 #
