V1FNL	;FUNCTION $LENGTH;YS-TS,V1FN,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1978
	S PASS=0,FAIL=0
	W !!,"V1FNL: TEST OF $LENGTH FUNCTION; $LENGTH(expr)",!
303	W !,"I-303  expr is string literal"
	S ITEM="I-303.1  all 95 ASCII printable character, including SP"
	S VCOMP=$LENGTH("ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890abcdefghijklmnopqrstuvwxyz !""#$%&'()*+,-./:;<=>?@[\]^_`{|}~")
	S VCORR=95 D EXAMINER
	;
	S ITEM="I-303.2  ""002""" S VCOMP=$L("002"),VCORR=3 D EXAMINER
	;
	S ITEM="I-303.3  strlit contains "" character",VCOMP=$L(""" "" """" """""" """) S VCORR=12 D EXAMINER
	;
	S ITEM="I-303.4  $L(expr)=256" S X="" F I=1:1:254 S X=X_"1"
	S VCOMP=$L(X)_" "_$L(X_"2"),VCORR="254 255" D EXAMINER
	;
304	W !,"I-304  expr is empty literal"
	S ITEM="I-304  " S VCOMP=$L("") S VCORR=0 D EXAMINER
	;
305	W !,"I-305  expr is control character"
	S ITEM="I-305.1  one control character",A=$C(0),VCOMP=$L(A),VCORR=1 D EXAMINER
	;
	S ITEM="I-305.2  all control characters"
	S A=$C(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,127)
	SET VCOMP=$L(A),VCORR=33 D EXAMINER
	;
306	W !,"I-306  expr contains operator"
	S ITEM="I-306.1  + unary operator" S VCOMP=$L(+"-123E") S VCORR=4 D EXAMINER
	S ITEM="I-306.2  + binary operator" S VCOMP=$L(123.45+"-12E+1") S VCORR=4 D EXAMINER
	S ITEM="I-306.3  _ binary operator" S VCOMP=$L(123.45_"-12E+1") S VCORR=12 D EXAMINER
	;
307	W !,"I-307  expr contains function"
	S ITEM="I-307  " S VCOMP=$L($L(000.11)+9) S VCORR=2 D EXAMINER
	;
308	W !,"I-308  expr is integer"
	S ITEM="I-308.1  123" S VCOMP=$L(123) S VCORR=3 D EXAMINER
	S ITEM="I-308.2  0" S VCOMP=$L(0) S VCORR=1 D EXAMINER
	S ITEM="I-308.3  -0.0" S VCOMP=$L(-0.0) S VCORR=1 D EXAMINER
	;
309	W !,"I-309  expr is non-integer"
	S ITEM="I-309.1  3000.11" S VCOMP=$L(3000.11),VCORR=7 D EXAMINER
	S ITEM="I-309.2  00030.011000" S VCOMP=$L(00030.011000),VCORR=6 D EXAMINER
	;
310	W !,"I-310  expr is negative numeric"
	S ITEM="I-310.1  -123" S VCOMP=$L(-123) S VCORR=4 D EXAMINER
	S ITEM="I-310.2  -123000" S VCOMP=$L(-123000) S VCORR=7 D EXAMINER
	S ITEM="I-310.3  -9.86056000" S VCOMP=$L(-9.86056000) S VCORR=8 D EXAMINER
	S ITEM="I-310.4  -000.0110" S VCOMP=$L(-000.0110),VCORR=5 D EXAMINER
	;
311	W !,"I-311  0<expr<1"
	S ITEM="I-311  000.1100" S VCOMP=$L(000.1100) S VCORR=3 D EXAMINER
	;
312	W !,"I-312  expr is numeric represented by scientific notation"
	S ITEM="I-312.1  -1E3" S VCOMP=$L(-1E3) S VCORR=5 D EXAMINER
	S ITEM="I-312.2  000001.0000E-6" S VCOMP=$L(000001.0000E-6) S VCORR=7 D EXAMINER
	S ITEM="I-312.3  98765E-3" S VCOMP=$L(98765E-3) S VCORR=6 D EXAMINER
	;
313	W !,"I-313  expr is numeric literal with leading zero"
	S ITEM="I-313.1  002" S VCOMP=$L(002) S VCORR=1 D EXAMINER
	S ITEM="I-313.2  0000000000000099.12" S VCOMP=$L(0000000000000099.12) S VCORR=5 D EXAMINER
	;
314	W !,"I-314  expr is decimal with following zero"
	S ITEM="I-314.1  3.110000000000000000",VCOMP=$L(3.110000000000000000) S VCORR=4 D EXAMINER
	S ITEM="I-314.2  3.1100000000000E-00002",VCOMP=$L(3.1100000000000E-00002),VCORR=5 D EXAMINER
	S ITEM="I-314.3  -00000000000000.0000000000000",VCOMP=$L(-00000000000000.0000000000000),VCORR=1 D EXAMINER
	;
END	W !!,"END OF V1FNL",!
	S ROUTINE="V1FNL",TESTS=29,AUTO=29,VISUAL=0 D ^VREPORT
	K  Q
	;
EXAMINER	I VCORR=VCOMP S PASS=PASS+1 W !,"   PASS  ",ITEM W:$Y>55 # Q
	S FAIL=FAIL+1 W !,"** FAIL  ",ITEM W:$Y>55 #
	W !,"           COMPUTED =""",VCOMP,"""" W:$Y>55 #
	W !,"           CORRECT  =""",VCORR,"""" W:$Y>55 #
	Q
