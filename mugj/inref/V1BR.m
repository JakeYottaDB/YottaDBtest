V1BR	;BREAK COMMAND;YS-KO-TS,V1BR,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1978
	S PASS=0,FAIL=0 W:$Y>50 #
0	W !!,"V1BR: TEST OF BREAK COMMAND"
	W !!,"'*BREAK*' WILL APPEAR IN EVERY OCCURENCE OF ACTIVE BREAK COMMAND."
	W !,"WHEN YOU MEET THIS MESSAGE, PLEASE EXECUTE THE COMMAND"
	W !," WHICH WOULD RESUME THE EXECUTION OF THIS ROUTINE."
	W !,"IF THE EXECUTION IS INTERRUPTED WITHOUT THIS MESSAGE,"
	W !," IT INDICATES THAT THERE IS SOMETHING WRONG WITH BREAK COMMAND."
	;
165	W !!,"I-165/166  breaking point and restarting point" W:$Y>50 #
	S ITEM="I-165/166  ",VCOMP=""
	S VCOMP=VCOMP_1 W !!,"TEST 1: *BREAK*" BREAK  S VCOMP=VCOMP_2
	S VCORR="12" D EXAMINER
	;
167	W !!,"I-167  post conditional" W:$Y>50 #
	S ITEM="I-167.1  postcondition is true"
	S VCOMP=1 W !!,"TEST 2: *BREAK* (B:1=1  )" B:1=1  S VCOMP=VCOMP_2
	S VCORR="12" D EXAMINER
	;
	S ITEM="I-167.2  postcondition is false" W:$Y>50 #
	S VCOMP=1 W !!,"TEST 3: *BREAK* (B:0  )" B:0  S VCOMP=VCOMP_2
	S VCORR=12 D EXAMINER
	;
168	W !!,"I-168  BREAK in internal routine in DO command" W:$Y>50 #
	S ITEM="I-168  "
	S VCOMP="A" W !!,"TEST 4: " D A S VCOMP=VCOMP_"D"
	S VCORR="ABCD" D EXAMINER
	;
169	W !!,"I-169  BREAK in external routine in DO command" W:$Y>50 #
	S ITEM="I-169  "
	S VCOMP="A" D ^V1BR1 S VCOMP=VCOMP_"D"
	S VCORR="ABCD" D EXAMINER
	;
170	W !!,"I-170  BREAK in FOR loop" W:$Y>50 #
	S ITEM="I-170  ",V=""
	F I=1:1:3 F J=1:1:3 S V=V_I_J_" " I I=2,J=2 S V=V_"$" W !!,"TEST 6: *BREAK*" B  S V=V_"! "
	S VCOMP=V,VCORR="11 12 13 21 22 $! 23 31 32 33 " D EXAMINER
	;
171	W !!,"I-171  BREAK in XECUTE command" W:$Y>50 #
	S ITEM="I-171  ",VCOMP=""
	S A="S VCOMP=VCOMP_1 X B S VCOMP=VCOMP_4",B="S VCOMP=VCOMP_2 W !!,""TEST 7: *BREAK*"" B  S VCOMP=VCOMP_3"
	X A S VCOMP=VCOMP_5
	S VCOMP=VCOMP_6
	S VCORR="123456" D EXAMINER
	;
END	W !!,"END OF V1BR",!
	S ROUTINE="V1BR",TESTS=7,AUTO=7,VISUAL=0 D ^VREPORT
	K  Q
	;
EXAMINER	I VCORR=VCOMP S PASS=PASS+1 W !,"   PASS  ",ITEM W:$Y>55 # Q
	S FAIL=FAIL+1 W !,"** FAIL  ",ITEM W:$Y>55 #
	W !,"           COMPUTED =""",VCOMP,"""" W:$Y>55 #
	W !,"           CORRECT  =""",VCORR,"""" W:$Y>55 #
	Q
	;
A	S VCOMP=VCOMP_"B" W "*BREAK* (B  )" B  S VCOMP=VCOMP_"C" Q
