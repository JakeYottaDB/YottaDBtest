V3NEWPN ;IW-KO-YS-TS,V3NEW,MVTS V9.10;15/6/96;
 ;COPYRIGHT MUMPS SYSTEMS LABORATORY 1990-1996
 W !!,"114---V3NEWPN: NEW -44-"
 W !!,"lvn is % and alphnumeric"
 ;
93 S ^ABSN="31000",^ITEM="III-1000  NEW all"
 S ^NEXT="94^V3NEWPN,V3NEWI1^V3NEW,V3ALDO^VV3" D ^V3PRESET K
 S ^VCOMP=""
 S %ABCDEFG(1,2,3)="%A"
 S %B123456("A","%",000)="%B"
 S %Z1D2E30("09793","ADEEDF")="%Z"
 D NEW1,CHECK
 S ^VCORR="1 0 1 0 1 0 1 0 %1%2%3%#10 1 10 1 10 1 0 0 %A%B%Z#"
 D ^VEXAMINE
 ;
94 S ^ABSN="31001",^ITEM="III-1001  selective NEW"
 S ^NEXT="95^V3NEWPN,V3NEWI1^V3NEW,V3ALDO^VV3" D ^V3PRESET K
 S ^VCOMP=""
 S %ABCDEFG="%A"
 S %B123456="%B"
 S %Z1D2E30="%Z"
 S %="%"
 D NEW2
 D CHECK
 S ^VCORR="0 0 0 0 1 0 1 0 %Z%#10 1 10 1 10 1 1 0 %a%b%z%#1 0 1 0 10 1 1 0 %A%B%z%#"
 D ^VEXAMINE
 ;
95 S ^ABSN="31002",^ITEM="III-1002  exclusive NEW"
 S ^NEXT="^V3NEWI1,V3NEWI2^V3NEW,V3ALDO^VV3" D ^V3PRESET K
 S ^VCOMP=""
 S %ABCDEFG="%A"
 S %B123456="%B"
 S %Z1D2E30="%Z"
 S %="%"
 S %ABCDEFG(1,2,3)="%A"
 S %B123456("A","%",000)="%B"
 S %Z1D2E30("09793","ADEEDF")="%Z"
 D NEW3
 D CHECK
 S ^VCORR="0 0 0 0 0 0 1 0 %#1 0 1 0 0 0 1 0 %A%B%#11 1 11 1 11 1 1 0 %A%A%B%B%Z%Z%#"
 D ^VEXAMINE
 ;
END W !!,"End of 114 --- V3NEWPN",!
 K  Q
 ;
NEW1 N
 S %ABCDEFG="%1",%B123456="%2",%Z1D2E30="%3",%="%"
 D CHECK Q
 ;
NEW2 ;
 NEW %ABCDEFG,%B123456
 D CHECK
 K %Z1D2E30
 S %ABCDEFG(1,2,3)="%a" S %B123456("A","%",000)="%b"
 S %Z1D2E30("09793","ADEEDF")="%z"
 D CHECK Q
 ;
NEW3 ;
 NEW (%)
 D CHECK
 S %ABCDEFG="%A"
 S %B123456="%B"
 D CHECK
 QUIT
 ;
CHECK ;
 S ^VCOMP=^VCOMP_$D(%ABCDEFG)_" "_$D(%ABCDEFG(1,2,3))_" "
 S ^VCOMP=^VCOMP_$D(%B123456)_" "_$D(%B123456("A","%",0))_" "
 S ^VCOMP=^VCOMP_$D(%Z1D2E30)_" "_$D(%Z1D2E30("09793","ADEEDF"))_" "
 S ^VCOMP=^VCOMP_$D(%)_" "_$D(%(2))_" "
 I $D(%ABCDEFG)#10=1    S ^VCOMP=^VCOMP_%ABCDEFG
 I $D(%ABCDEFG(1,2,3))#10=1 S ^VCOMP=^VCOMP_%ABCDEFG(1,2,3)
 I $D(%B123456)#10=1    S ^VCOMP=^VCOMP_%B123456
 I $D(%B123456("A","%",0))#10=1 S ^VCOMP=^VCOMP_%B123456("A","%",0)
 I $D(%Z1D2E30)#10=1    S ^VCOMP=^VCOMP_%Z1D2E30
 I $D(%Z1D2E30("09793","ADEEDF"))#10=1 S ^VCOMP=^VCOMP_%Z1D2E30("09793","ADEEDF")
 I $D(%)#10=1    S ^VCOMP=^VCOMP_%
 I $D(%(2))#10=1 S ^VCOMP=^VCOMP_%(2)
 S ^VCOMP=^VCOMP_"#"
 Q
 ;
SUM S SUM=0 F I=1:1 S L=$T(+I) Q:L=""  F K=1:1:$L(L) S SUM=SUM+$A(L,K)
 Q
