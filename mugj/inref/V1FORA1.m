V1FORA1	;FOR COMMAND -1.1-;YS-TS,V1FORA,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1978
	S PASS=0,FAIL=0
	W !!,"V1FORA1: TEST OF FOR COMMAND -1.1-",!
	W !,"FOR lvn=expr",!
335	W !,"I-335  expr is intlit"
	S ITEM="I-335.1  lvn does not exist",VCOMP="" K I F I=7 S VCOMP=VCOMP_I
	S VCOMP=VCOMP_I S VCORR="77" D EXAMINER
	;
	S ITEM="I-335.2  lvn exist",VCOMP="",%=0 F %=98765432 S VCOMP=VCOMP_%
	S VCOMP=VCOMP_% S VCORR="9876543298765432" D EXAMINER
	;
336	W !,"I-336  expr is numlit"
	S ITEM="I-336  ",VCOMP="",I=0.0 F I=7.5 S VCOMP=VCOMP_I
	S VCOMP=VCOMP_I S VCORR="7.57.5" D EXAMINER
	;
337	W !,"I-337  expr is strlit"
	S ITEM="I-337  ",VCOMP="" K I F I="\^-0" S VCOMP=VCOMP_I
	S VCOMP=VCOMP_I S VCORR="\^-0\^-0" D EXAMINER
	;
	W !!,"FOR lvn=numexpr1:numexpr2:numexpr3",!
338	W !,"I-338/339/342  numexpr2>0"
	S ITEM="I-338/339/342.1  numexpr1<numexpr3",VCOMP="" F I=1:1:9 S VCOMP=VCOMP_I
	S VCOMP=VCOMP_I S VCORR="1234567899" D EXAMINER
	;
	S ITEM="I-338/339/342.2  numexpr1=numexpr3",VCOMP="" F I=2:1:2 S VCOMP=VCOMP_I
	S VCOMP=VCOMP_I S VCORR="22" D EXAMINER
	;
	S ITEM="I-338/339/342.3  numexpr1>numexpr3",VCOMP="" S I=0 F I=5:10:2 S VCOMP=VCOMP_I
	S VCOMP=VCOMP_I S VCORR="5" D EXAMINER
	;
340	W !,"I-340  numexpr2=0"
	S ITEM="I-340.1  numexpr1<numexpr3",VCOMP="" S I=0 F J=4:0:5 S I=I+1 S VCOMP=VCOMP_J I I=4 G G3401
	S VCOMP=VCOMP_"!!"
G3401	S VCOMP=VCOMP_J,VCORR="44444" D EXAMINER
	;
	S ITEM="I-340.2  numexpr1=numexpr3",VCOMP="" S I=0 F J=3:0:3 S I=I+1,VCOMP=VCOMP_J I I=3 G G3402
	S VCOMP=VCOMP_"!!"
G3402	S VCOMP=VCOMP_J,VCORR="3333" D EXAMINER
	;
	S ITEM="I-340.3  numexpr1>numexpr3",VCOMP="" S I=0 F J=3:0:2.9 S I=I+1 S VCOMP=VCOMP_J I I=3 G G3403
G3403	S VCOMP=VCOMP_J,VCORR="3" D EXAMINER
	;
	S ITEM="I-340.4  numexpr1<numexpr3 another",VCOMP="",I=12 FOR I=-4:0:5.3 S VCOMP=VCOMP_I,I=I+2,VCOMP=VCOMP_I
	S VCOMP=VCOMP_I,VCORR="-4-2-200224466" D EXAMINER
	;
341	W !,"I-341/343  numexpr2<0"
	S ITEM="I-341/343.1  numexpr1<numexpr3",VCOMP="" F I=4:-2:5555555 S VCOMP=VCOMP_I
	S VCOMP=VCOMP_I,VCORR="4" D EXAMINER
	;
	S ITEM="I-341/343.2  numexpr1=numexpr3",VCOMP="" F I=4:-2:4 S VCOMP=VCOMP_I
	S VCOMP=VCOMP_I,VCORR="44" D EXAMINER
	;
	S ITEM="I-341/343.3  numexpr1>numexpr3",VCOMP="" F I=4:-2:-5 S VCOMP=VCOMP_I
	S VCOMP=VCOMP_I,VCORR="420-2-4-4" D EXAMINER
	;
344	W !,"I-344  numexpr1=numexpr2=numexpr3"
	S ITEM="I-344.1  numexpr>0",VCOMP="" F I=10:10:10 S VCOMP=VCOMP_I
	S VCOMP=VCOMP_I,VCORR="1010" D EXAMINER
	;
	S ITEM="I-344.2  numexpr=0",VCOMP="" S J=0 F I=0:000:.0 S VCOMP=VCOMP_I,J=J+1 I J=3 S I=4
	S VCOMP=VCOMP_I,VCORR="0004" D EXAMINER
	;
	S ITEM="I-344.3  numexpr<0",VCOMP="" F I=-12:-12.0:"-12" S VCOMP=VCOMP_I
	S VCOMP=VCOMP_I,VCORR="-12-12" D EXAMINER
	;
END	W !!,"END OF V1FORA1",!
	S ROUTINE="V1FORA1",TESTS=17,AUTO=17,VISUAL=0 D ^VREPORT
	K  Q
	;
EXAMINER	I VCORR=VCOMP S PASS=PASS+1 W !,"   PASS  ",ITEM W:$Y>55 # Q
	S FAIL=FAIL+1 W !,"** FAIL  ",ITEM W:$Y>55 #
	W !,"           COMPUTED =""",VCOMP,"""" W:$Y>55 #
	W !,"           CORRECT  =""",VCORR,"""" W:$Y>55 #
