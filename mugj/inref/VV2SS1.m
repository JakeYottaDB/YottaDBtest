VV2SS1	;STRING SUBSCRIPT -1-;KO-TS,,VALIDATION VERSION 7.1;31-AUG-1987;
	;COPYRIGHT MUMPS SYSTEM LABORATORY 1984
	W !!,"VV2SS1: TEST OF STRING SUBSCRIPT -1-",!
	S PASS=0,FAIL=0 S MAX="#" F I=0:2:252 S MAX=MAX_"QW"
171	W !,"II-171  Local variable primitive sequence"
	S ITEM="II-171  ",VCOMP="" FOR I=126:-1:32 S A($C(I))=$C(I)
	S X="" F I=1:1 S Y=$O(A(X)) Q:Y=""  S VCOMP=VCOMP_($A(Y)-$A(X)) S X=Y
	S VCORR="49111111111-25" F I=1:1:85 S VCORR=VCORR_1
	D EXAMINER
	;
172	W !,"II-172  Global variable primitive sequence"
	S ITEM="II-172  ",VCOMP="" K ^VV F I=32:1:126 S ^VV($C(I))=$C(I)
	S X="" F I=1:1 S Y=$O(^VV(X)) Q:Y=""  S VCOMP=VCOMP_($A(Y)-$A(X)) S X=Y
	S VCORR="49111111111-25" F I=1:1:85 S VCORR=VCORR_1
	D EXAMINER
	;
173	W !,"II-173  Length of local variable subscript is 31 characters"
	S ITEM="II-173  ",VCOMP=""
	S ABC("ABCDEFGHIJKLMNOPQRSTUVWXYZ#$%&'")=1
	S VCOMP=ABC("ABCDEFGHIJKLMNOPQRSTUVWXYZ#$%&'")
	S ABC("ABCDEFGHIJKLMNOPQRSTUVWXYZ#$%&'")=MAX
	S VCOMP=VCOMP_(ABC("ABCDEFGHIJKLMNOPQRSTUVWXYZ#$%&'")=MAX) K ABC
	S VCORR="11" D EXAMINER
	;
174	W !,"II-174  Length of local variable is 63 characters"
	S ITEM="II-174  ",VCOMP=""
	S ABCDEFGH("ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ")=2
	S VCOMP=ABCDEFGH("ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ")
	S ABCDEFGH("ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ")=MAX
	S VCOMP=VCOMP_(ABCDEFGH("ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ")=MAX) K ABCDEFGH
	S VCORR="21" D EXAMINER
	;
175	W !,"II-175  Length of global variable subscript is 31 characters"
	S ITEM="II-175  ",VCOMP=""
	S ^VV("ABCDEFGHIJKLMNOPQRSTUVWXYZ#$%&'")=3
	S VCOMP=^VV("ABCDEFGHIJKLMNOPQRSTUVWXYZ#$%&'")
	S ^VV("ABCDEFGHIJKLMNOPQRSTUVWXYZ#$%&'")=MAX
	S VCOMP=VCOMP_(^VV("ABCDEFGHIJKLMNOPQRSTUVWXYZ#$%&'")=MAX)
	S VCORR="31" D EXAMINER
	;
176	W !,"II-176  Length of global variable is 63 characters"
	S ITEM="II-176  ",VCOMP=""
	S ^VV("ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDE")=4
	S VCOMP=^VV("ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDE")
	S ^VV("ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDE")=MAX
	S VCOMP=VCOMP_(^VV("ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDEFGHIJ","ABCDE")=MAX)
	S VCORR="41" D EXAMINER
	;
END	W !!,"END OF VV2SS1",!
	S ROUTINE="VV2SS1",TESTS=6,AUTO=6,VISUAL=0 D ^VREPORT
	K  K ^VV Q
	;
EXAMINER	I VCORR=VCOMP S PASS=PASS+1 W !,"   PASS  ",ITEM W:$Y>55 # Q
	S FAIL=FAIL+1 W !,"** FAIL  ",ITEM W:$Y>55 #
	W !,"           COMPUTED =""",VCOMP,"""" W:$Y>55 #
	W !,"           CORRECT  =""",VCORR,"""" W:$Y>55 #
	Q
