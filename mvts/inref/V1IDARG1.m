V1IDARG1 ;IW-KO-MM-YS-TS,V1IDARG,MVTS V9.10;15/6/96;ARGUMENT LEVEL INDIRECTION -1-
 ;COPYRIGHT MUMPS SYSTEMS LABORATORY 1978-1996
 W !!,"153---V1IDARG1: Argument level indirection -1-",!
IF W !,"IF command",!
417 S ^ABSN="11848",^ITEM="I-417  Indirection of ifargument",^NEXT="418^V1IDARG1,V1IDARG2^V1IDARG,V1XECA^VV1" W !,^ITEM D ^V1PRESET
 S VCOMP="" S A=1 I @A S VCOMP=1 I  S VCOMP=VCOMP_" "_$T_"/"
 E  S VCOMP=VCOMP_"ERROR 417.1"
 S A="1=0" I @A S VCOMP=VCOMP_"ERROR 417.2"
 E  S VCOMP=VCOMP_2
 S VCOMP=VCOMP_" "_$T_"/"
 S B="1=1",D="B" I @D S VCOMP=VCOMP_3 I  S VCOMP=VCOMP_" "_$T
 E  S VCOMP=VCOMP_"ERROR 417.3"
 S ^VCOMP=VCOMP,^VCORR="1 1/2 0/3 1" D ^VEXAMINE
 ;
418 S ^ABSN="11849",^ITEM="I-418  Indirection of ifargument list",^NEXT="419^V1IDARG1,V1IDARG2^V1IDARG,V1XECA^VV1" W !,^ITEM D ^V1PRESET
 S VCOMP=""
 S A="1=1,0" I @A S VCOMP=VCOMP_"ERROR 418 " I  S VCOMP=VCOMP_"ERROR 418.1 "
 E  S VCOMP=VCOMP_"418 "_$T
 S ^VCOMP=VCOMP,^VCORR="418 0" D ^VEXAMINE
 ;
419 S ^ABSN="11850",^ITEM="I-419  List of indirection and ifargument",^NEXT="420^V1IDARG1,V1IDARG2^V1IDARG,V1XECA^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP=""
 S B="00.1,2",C="$E(123,2)",D="1,0"
 IF 1.0,B,@B,@C S ^VCOMP=^VCOMP_"419.1 "_$T
 E  S ^VCOMP=^VCOMP_" ERROR 419.1 "_$T
 IF +D,2>1,@B,@C,@D S ^VCOMP=^VCOMP_" ERROR 419.2 "_$T
 E  S ^VCOMP=^VCOMP_" 419.2 "_$T
 S ^VCORR="419.1 1 419.2 0" D ^VEXAMINE
 ;
420 S ^ABSN="11851",^ITEM="I-420  2 levels of ifargument indirection",^NEXT="421^V1IDARG1,V1IDARG2^V1IDARG,V1XECA^VV1" W !,^ITEM D ^V1PRESET
 S V=""
 S A="@A(1)",A(1)="$E(A(2),2,3)+0",A(2)=9876
 S ^V1A(100)="^V1A(2)",^(2)="^(3)=""ABCD""",^(3)="ABCD"
 I @A S V=V_"4201 " I @@^V1A(100) S V=V_"42011 "
 E  S V=V_" ERROR 420.1 "
 S V=V_$T_"/"
 S A(2)=2000 I @A S V=V_" ERROR 420.2 "
 E  S V=V_"4202 "
 S V=V_$T
 S ^VCOMP=V,^VCORR="4201 42011 1/4202 0" D ^VEXAMINE
 ;
421 S ^ABSN="11852",^ITEM="I-421  3 levels of ifargument indirection",^NEXT="422^V1IDARG1,V1IDARG2^V1IDARG,V1XECA^VV1" W !,^ITEM D ^V1PRESET
 S VCOMP=""
 S A="@A(1)",A(1)="@A(3)",A(3)="153.456E-2>A(4)",A(4)="0001.5000"
 K ^V1A S ^V1A(2)="B(2)",B(2)="^V1A(3)",^V1A(3)="""AB23E""?2A.N1A"
 I @A,0.002 S VCOMP=VCOMP_"AA " I 1,@@@^V1A(2) S VCOMP=VCOMP_"BB "
 E  S VCOMP=VCOMP_"E1 "
 S ^VCOMP=VCOMP,^VCORR="AA BB " D ^VEXAMINE
 ;
422 S ^ABSN="11853",^ITEM="I-422  Value of indirection expratom contains operator",^NEXT="423^V1IDARG1,V1IDARG2^V1IDARG,V1XECA^VV1" W !,^ITEM D ^V1PRESET
 S ^VCOMP="" K ^V1A
 S ^V1A(101)="""DOG""[^V1A(3)",^V1A(3)="G"
 I @^V1A(101) S ^VCOMP=^VCOMP_"DOGS "
 E  S ^VCOMP=^VCOMP_"CATS "
 S ^V1A(3)="C" I @^V1A(101) S ^VCOMP=^VCOMP_"NOT CATS"
 E  S ^VCOMP=^VCOMP_"CATS"
 S ^VCORR="DOGS CATS" D ^VEXAMINE
 ;
423 S ^ABSN="11854",^ITEM="I-423  Value of indirection expratom is function",^NEXT="424^V1IDARG1,V1IDARG2^V1IDARG,V1XECA^VV1" W !,^ITEM D ^V1PRESET
 S VCOMP="" S A(1,1,1)="$L($P(""A-B-CCC-EFD-55555"",@B,I))=I",B="C",C="-"
 F I=1:1:5 I @A(1,1,1) S VCOMP=VCOMP_I_" "
 S ^VCOMP=VCOMP,^VCORR="1 3 5 " D ^VEXAMINE
 ;
424 S ^ABSN="11855",^ITEM="I-424  Value of indirection expratom contains indirection",^NEXT="425^V1IDARG1,V1IDARG2^V1IDARG,V1XECA^VV1" W !,^ITEM D ^V1PRESET
 S VCOMP=""
 S A(1)="123=@B(@A(2))",B(1)="@C",C="D",D=22,A(2)="A(3)",A(3)=1
 I @A(@A(2)) S VCOMP=VCOMP_"A "
 E  S VCOMP=VCOMP_"AAE "
 S ^VCOMP=VCOMP,^VCORR="AAE " D ^VEXAMINE
 ;
425 S ^ABSN="11856",^ITEM="I-425  Value of indirection expratom subscripted variable name",^NEXT="V1IDARG2^V1IDARG,V1XECA^VV1" W !,^ITEM D ^V1PRESET
 S VCOMP=""
 S A(1)="A(@B,@B(2)),@B",B="B(1)",B(1)=2,B(2)="@B(3)",B(3)="B(4)",B(4)=3
 S A(2,3)=0.23E-3 I @A(1) S VCOMP=VCOMP_"A "
 E  S VCOMP=VCOMP_"AAE "
 S A(2,3)="0.00E+2=1" IF @A(1) S VCOMP=VCOMP_"BBE "
 E  S VCOMP=VCOMP_"B "
 S ^VCOMP=VCOMP,^VCORR="A B " D ^VEXAMINE
 ;
END W !!,"End of 153---V1IDARG1",!
 K  K ^V1A Q
SUM S SUM=0 F I=1:1 S L=$T(+I) Q:L=""  F K=1:1:$L(L) S SUM=SUM+$A(L,K)
 Q
